
module crm_module
  use task_init_mod, only: task_init
  use abcoefs_mod, only: abcoefs
  use kurant_mod, only: kurant
  use setperturb_mod, only: setperturb
  use boundaries_mod, only: boundaries
  use forcing_mod, only: forcing
  use advect_mom_mod, only: advect_mom
  use adams_mod, only: adams
  use advect_all_scalars_mod, only: advect_all_scalars
  use sat_mod
  use crmsurface_mod
#ifdef sam1mom
  use precip_init_mod
#endif
  use zero_mod
  use buoyancy_mod
  use pressure_mod
  use uvw_mod
  use diagnose_mod
  use damping_mod
  use ice_fall_mod
  use coriolis_mod

  use crm_state_module, only: crm_state_type
  use crm_rad_module, only: crm_rad_type
  use crm_input_module, only: crm_input_type
  use crm_output_module, only: crm_output_type

!---------------------------------------------------------------
!  Super-parameterization's main driver
!  Marat Khairoutdinov, 2001-2009
!---------------------------------------------------------------
use setparm_mod, only : setparm

contains

subroutine crm(lchnk,         icol,             ncrms,            &
               phys_stage,    dt_gl,            plev,             &
#ifdef CLUBB_CRM
               clubb_buffer,  crm_cld,                            &
               clubb_tk,      clubb_tkh,                          &
               relvar,        accre_enhan,      qclvar,           &
#endif
#ifdef ECPP
               abnd,          abnd_tf,          massflxbnd,       &
               acen,          acen_tf,                            &
               rhcen,         qcloudcen,        qicecen,          &
               qlsinkcen,     precrcen,         precsolidcen,     &
               qlsink_bfcen,  qlsink_avgcen,    praincen,         &
               wupthresh_bnd, wdownthresh_bnd,  wwqui_cen,        &
               wwqui_bnd,     wwqui_cloudy_cen, wwqui_cloudy_bnd, &
#endif
               crm_input,     crm_state,        crm_rad,          &
               crm_output)
        !---------------------------------------------------------------
    use crm_dump              , only: crm_dump_input, crm_dump_output
    use shr_kind_mod          , only: r8 => shr_kind_r8
    !MRN: phys_grid is a rabbit hole of dependencies I'd rather hijack and avoid.
    !MRN: It's only used to get the longitude and latitude for each call and then
    !MRN: random seed values for perturbations to the initial temprature field on
    !MRN: the first call to crm(...)

#ifndef CRM_STANDALONE
    use phys_grid             , only: get_rlon_p, get_rlat_p, get_gcol_p  !, get_gcol_all_p
#endif
    use ppgrid                , only: pcols
    use vars
    use params
    use microphysics
    use sgs
    use crmtracers
    use scalar_momentum_mod

! whannah - Matt Norman added the more specific use statements below - however this was causing problems for the 1-moment configuration
!
!     use vars                  , only: crm_nx, crm_ny, crm_nz, nx, ny, nz, crm_dx, crm_dt, fcory, fcorzy, latitude, longitude, z, zi, pres, prespot, bet, gamaz, &
!                                       adzw, adz, rho, rhow, u, v, w, tabs, dudt, dvdt, dwdt, p, wsub, cf3d, u0, v0, t0, tabs0, q0, t, qp0, tke0, qv0, qn0,  &
!                                       tke0, ttend, qtend, utend, vtend, ug0, vg0, tg0, qg0, qtotmicro, dt3, precsfc, precssfc, qpfall, precflux, uwle, uwsb, &
!                                       vwle, vwsb, dostatis, nzm, dz, yes3d, qcl, qci, qpl, qpi, qv, fluxbu, fluxbv, fluxbt, fluxbq, fluxtu, fluxtv, fluxtt, &
!                                       fluxtq, fzero, uwle, uwsb, vwle, nstop, dt, nsave3d, nstep, nprint, ncycle, day, day0, time, icycle, dtn, na, dtfactor, &
! #ifdef MODAL_AERO
!                                       naer, vaer, hgaer, &
! #endif
!                                       nc, nb, qsatw_crm
!     use params                , only: latitude0, longitude0, fcor, pi, fcorz, ocean, land, rgas, cp, fac_fus, uhl, vhl, z0, dodamping, dosurface, docoriolis, &
!                                       taux0, tauy0, doclubb, doclubbnoninter, docloud, dosmoke, crm_rknd
!     use microphysics          , only: nmicro_fields, micro_field, cloudliq, aut1a, acc1a, evpc1a, evpr1a, mlt1a, sub1a, dep1a, con1a, mkwsb, mkwle, mkadv, &
!                                       mkdiff, qpsrc, qpevp, ggr, dopredictnc, incl, nc0, fac_cond, fac_sub, iqr, iqs, iqg, inci, ins, wvar, aut1, acc1,    &
!                                       evpc1, evpr1, mlt1, sub1, dep1, con1, inr, ing, iqv, iqci, micro_init, micro_proc
!     use sgs                   , only: tke, tk, tkh, dosgs, sgs_init, sgs_proc, sgs_mom, sgs_scalars

#ifdef MODAL_AERO
    use modal_aero_data       , only: ntot_amode
#endif
    use crmdims               , only: nclubbvars, crm_nx_rad, crm_ny_rad
#ifdef CLUBB_CRM
    use clubb_sgs             , only: advance_clubb_sgs, clubb_sgs_setup, clubb_sgs_cleanup, apply_clubb_sgs_tndcy, apply_clubb_sgs_tndcy_scalars, &
                                      apply_clubb_sgs_tndcy_mom, t2thetal, total_energy
    use clubb_precision       , only: time_precision, core_rknd
    use clubbvars             , only: up2, vp2, wprtp, wpthlp, wp2, wp3, rtp2, thlp2, rtpthlp, upwp, vpwp, cloud_frac, t_tndcy, qc_tndcy, qv_tndcy, &
                                      u_tndcy, v_tndcy, lrestart_clubb, rho_ds_zt, rho_ds_zm, thv_ds_zt, thv_ds_zm, invrs_rho_ds_zt, invrs_rho_ds_zm, &
                                      tracer_tndcy, sclrp2, sclrprtp, sclrpthlp, wpsclrp, relvarg, accre_enhang, qclvarg, edsclr_dim, sclr_dim, rho_ds_zt, &
                                      rho_ds_zm, rtm_spurious_source, thlm_spurious_source
    use fill_holes            , only: vertical_integral
    use numerical_check       , only: calculate_spurious_source
    use grid_class            , only: gr
#endif
#ifdef ECPP
    use ecppvars              , only: qlsink, precr, precsolid, &
                                      area_bnd_final, area_bnd_sum, area_cen_final, area_cen_sum, &
                                      mass_bnd_final, mass_bnd_sum, rh_cen_sum, qcloud_cen_sum, qice_cen_sum, &
                                      qlsink_cen_sum, precr_cen_sum, precsolid_cen_sum, xkhvsum, wup_thresh, wdown_thresh, &
                                      wwqui_cen_sum, wwqui_bnd_sum, wwqui_cloudy_cen_sum, wwqui_cloudy_bnd_sum, &
                                      qlsink_bf_cen_sum, qlsink_avg_cen_sum, prain_cen_sum, qlsink_bf, prain
    use module_ecpp_crm_driver, only: ecpp_crm_stat, ecpp_crm_init, ecpp_crm_cleanup, ntavg1_ss, ntavg2_ss
    use ecppvars              , only: NCLASS_CL, ncls_ecpp_in, NCLASS_PR
#endif /* ECPP */
    use cam_abortutils        , only: endrun
    use time_manager          , only: get_nstep

    implicit none
    integer , intent(in   ) :: lchnk                            ! chunk identifier (only for lat/lon and random seed)
    integer , intent(in   ) :: ncrms                            ! Number of "vector" GCM columns to push down into CRM for SIMD vectorization / more threading
    integer , intent(in   ) :: phys_stage                       ! physics run stage indicator (1 or 2 = bc or ac)
    integer , intent(in   ) :: plev                             ! number of levels in parent model
    real(r8), intent(in   ) :: dt_gl                            ! global model's time step
    integer , intent(in   ) :: icol                (ncrms)      ! column identifier (only for lat/lon and random seed)
    type(crm_state_type), intent(inout) :: crm_state
    type(crm_rad_type),   intent(inout) :: crm_rad
    type(crm_input_type), intent(in   ) :: crm_input
    type(crm_output_type), intent(inout) :: crm_output
#ifdef CLUBB_CRM
    real(r8), intent(inout), target :: clubb_buffer(ncrms,crm_nx, crm_ny, crm_nz+1,1:nclubbvars)
    real(r8), intent(  out) :: crm_cld             (ncrms,crm_nx, crm_ny, crm_nz+1)
    real(r8), intent(  out) :: clubb_tk            (ncrms,crm_nx, crm_ny, crm_nz)
    real(r8), intent(  out) :: clubb_tkh           (ncrms,crm_nx, crm_ny, crm_nz)
    real(r8), intent(  out) :: relvar              (ncrms,crm_nx, crm_ny, crm_nz)
    real(r8), intent(  out) :: accre_enhan         (ncrms,crm_nx, crm_ny, crm_nz)
    real(r8), intent(  out) :: qclvar              (ncrms,crm_nx, crm_ny, crm_nz)
#endif /* CLUBB_CRM */
    real(r8)                :: dd_crm              (ncrms,plev)       ! mass entraiment from downdraft
    real(r8)                :: mui_crm             (ncrms,plev+1)     ! mass flux up at the interface
    real(r8)                :: mdi_crm             (ncrms,plev+1)     ! mass flux down at the interface
#ifdef ECPP
    real(r8), intent(  out) :: acen                (ncrms,plev,NCLASS_CL,ncls_ecpp_in,NCLASS_PR)  ! cloud fraction for each sub-sub class for full time period
    real(r8), intent(  out) :: acen_tf             (ncrms,plev,NCLASS_CL,ncls_ecpp_in,NCLASS_PR)  ! cloud fraction for end-portion of time period
    real(r8), intent(  out) :: rhcen               (ncrms,plev,NCLASS_CL,ncls_ecpp_in,NCLASS_PR)  ! relative humidity (0-1)
    real(r8), intent(  out) :: qcloudcen           (ncrms,plev,NCLASS_CL,ncls_ecpp_in,NCLASS_PR)  ! cloud water (kg/kg)
    real(r8), intent(  out) :: qicecen             (ncrms,plev,NCLASS_CL,ncls_ecpp_in,NCLASS_PR)  ! cloud ice (kg/kg)
    real(r8), intent(  out) :: qlsinkcen           (ncrms,plev,NCLASS_CL,ncls_ecpp_in,NCLASS_PR)  ! cloud water loss rate from precipitation (/s??)
    real(r8), intent(  out) :: precrcen            (ncrms,plev,NCLASS_CL,ncls_ecpp_in,NCLASS_PR)  ! liquid (rain) precipitation rate (kg/m2/s)
    real(r8), intent(  out) :: precsolidcen        (ncrms,plev,NCLASS_CL,ncls_ecpp_in,NCLASS_PR)  ! solid (rain) precipitation rate (kg/m2/s)
    real(r8), intent(  out) :: qlsink_bfcen        (ncrms,plev,NCLASS_CL,ncls_ecpp_in,NCLASS_PR)  ! cloud water loss rate from precipitation calculated
                                                                                                   ! cloud water before precipitatinog (/s)
    real(r8), intent(  out) :: qlsink_avgcen       (ncrms,plev,NCLASS_CL,ncls_ecpp_in,NCLASS_PR)  ! cloud water loss rate from precipitation calculated
                                                                                                   ! from praincen and qlcoudcen averaged over
                                                                                                   ! ntavg1_ss time step (/s??)
    real(r8), intent(  out) :: praincen            (ncrms,plev,NCLASS_CL,ncls_ecpp_in,NCLASS_PR)  ! cloud water loss rate from precipitation (kg/kg/s)
    real(r8), intent(  out) :: wwqui_cen           (ncrms,plev)                                   ! vertical velocity variance in quiescent class (m2/s2)
    real(r8), intent(  out) :: wwqui_cloudy_cen    (ncrms,plev)                                   ! vertical velocity variance in quiescent, and cloudy class (m2/s2) at layer boundary
    real(r8), intent(  out) :: abnd                (ncrms,plev+1,NCLASS_CL,ncls_ecpp_in,NCLASS_PR)! cloud fraction for each sub-sub class for full time period
    real(r8), intent(  out) :: abnd_tf             (ncrms,plev+1,NCLASS_CL,ncls_ecpp_in,NCLASS_PR)! cloud fraction for end-portion of time period
    real(r8), intent(  out) :: massflxbnd          (ncrms,plev+1,NCLASS_CL,ncls_ecpp_in,NCLASS_PR)! sub-class vertical mass flux (kg/m2/s) at layer bottom boundary.
    real(r8), intent(  out) :: wupthresh_bnd       (ncrms,plev+1)                                 ! vertical velocity threshold for updraft (m/s)
    real(r8), intent(  out) :: wdownthresh_bnd     (ncrms,plev+1)                                 ! vertical velocity threshold for downdraft (m/s)
    real(r8), intent(  out) :: wwqui_bnd           (ncrms,plev+1)                                 ! vertical velocity variance in quiescent class (m2/s2)
    real(r8), intent(  out) :: wwqui_cloudy_bnd    (ncrms,plev+1)                                 ! vertical velocity variance in quiescent, and cloudy class (m2/s2)
#endif /* ECPP */

    !  Local space:
    real(r8) :: qtot(ncrms,20)  ! Total water for water conservation check
    real(r8),       parameter :: umax = 0.5*crm_dx/crm_dt       ! maxumum ampitude of the l.s. wind
    real(r8),       parameter :: wmin = 2.                      ! minimum up/downdraft velocity for stat
    real(crm_rknd), parameter :: cwp_threshold = 0.001          ! threshold for cloud condensate for shaded fraction calculation
    integer,        parameter :: perturb_seed_scale = 1000      ! scaling value for setperturb() seed value (seed = gcol * perturb_seed_scale)
    real(r8)        :: crm_run_time                             ! length of CRM integration (=dt_gl*0.5 if SP_CRM_SPLIT is defined)
    real(r8)        :: icrm_run_time                            ! = 1 / crm_run_time
    real(r8)        :: factor_xy, factor_xyt, idt_gl
    real(crm_rknd)  :: tmp1, tmp2
    real(crm_rknd)  :: u2z,v2z,w2z
    integer         :: i,j,k,l,ptop,nn,icyc, nstatsteps, icrm
    integer         :: kx
    real(crm_rknd)  :: ustar, bflx, wnd, qsat, omg
    real(crm_rknd)  :: colprec,colprecs
    real(r8)        :: zs                ! surface elevation
    integer         :: igstep            ! GCM time steps
    integer         :: iseed             ! seed for random perturbation
    real(crm_rknd)  :: ntotal_step
    integer         :: myrank, ierr
    ! whannah - variables for new radiation group method
    real(crm_rknd) :: crm_nx_rad_fac
    real(crm_rknd) :: crm_ny_rad_fac
    integer        :: i_rad
    integer        :: j_rad
    !Arrays
    real(crm_rknd), allocatable :: dummy(:)
    real(crm_rknd), allocatable :: t00(:)
    real(crm_rknd), allocatable :: fluxbtmp(:,:)
    real(crm_rknd), allocatable :: fluxttmp(:,:)    !bloss
    real(crm_rknd), allocatable :: tln  (:)
    real(crm_rknd), allocatable :: qln  (:)
    real(crm_rknd), allocatable :: qccln(:)
    real(crm_rknd), allocatable :: qiiln(:)
    real(crm_rknd), allocatable :: uln  (:)
    real(crm_rknd), allocatable :: vln  (:)
#if defined(SP_ESMT)
    real(crm_rknd), allocatable  :: uln_esmt(:)
    real(crm_rknd), allocatable  :: vln_esmt(:)     ! tempoerary variables for expliciit scalar momentum transport
#endif
    real(crm_rknd), allocatable  :: cwp     (:,:)
    real(crm_rknd), allocatable  :: cwph    (:,:)
    real(crm_rknd), allocatable  :: cwpm    (:,:)
    real(crm_rknd), allocatable  :: cwpl    (:,:)
    logical       , allocatable  :: flag_top(:,:)
    real(crm_rknd), allocatable  :: cltemp  (:,:)
    real(crm_rknd), allocatable  :: cmtemp  (:,:)
    real(crm_rknd), allocatable  :: chtemp  (:,:)
    real(crm_rknd), allocatable  :: cttemp  (:,:)
    integer       , allocatable  :: gcolindex(:)  ! array of global latitude indices
#ifdef CLUBB_CRM
    !Array indicies for spurious RTM check
    real(kind=core_rknd), allocatable :: thlm_flux_top, thlm_flux_sfc, rtm_flux_top, rtm_flux_sfc
    real(kind=core_rknd), allocatable :: rtm_integral_before (:,:)
    real(kind=core_rknd), allocatable :: rtm_integral_after  (:,:)
    real(kind=core_rknd), allocatable :: thlm_integral_before(:,:)
    real(kind=core_rknd), allocatable :: thlm_integral_after (:,:)
    real(kind=core_rknd), allocatable :: thlm_before(:)
    real(kind=core_rknd), allocatable :: thlm_after (:)
    real(kind=core_rknd), allocatable :: rtm_column (:) ! Total water (vapor + liquid)     [kg/kg]
#endif /* CLUBB_CRM */
    real(crm_rknd) :: zeroval

    allocate( dummy(nz) )
    allocate( t00(nz) )
    allocate( fluxbtmp(nx,ny) )
    allocate( fluxttmp(nx,ny) )
    allocate( tln(plev) )
    allocate( qln(plev) )
    allocate( qccln(plev) )
    allocate( qiiln(plev) )
    allocate( uln(plev) )
    allocate( vln(plev) )
#if defined(SP_ESMT)
    allocate( uln_esmt(plev) )
    allocate( vln_esmt(plev) )
#endif
    allocate( cwp(nx,ny) )
    allocate( cwph(nx,ny) )
    allocate( cwpm(nx,ny) )
    allocate( cwpl(nx,ny) )
    allocate( flag_top(nx,ny) )
    allocate( gcolindex(pcols) )
    allocate( cltemp(nx,ny) )
    allocate( cmtemp(nx,ny) )
    allocate( chtemp(nx,ny) )
    allocate( cttemp(nx,ny) )
#ifdef CLUBB_CRM
    allocate( rtm_integral_before (nx,ny) )
    allocate( rtm_integral_after (nx,ny) )
    allocate( thlm_integral_before(nx,ny) )
    allocate( thlm_integral_after(nx,ny) )
    allocate( thlm_before(nzm) )
    allocate( thlm_after(nzm) )
    allocate( rtm_column(nzm) )
#endif /* CLUBB_CRM */

    zeroval = 0

    dummy  = zeroval
    t00  = zeroval
    fluxbtmp  = zeroval
    fluxttmp  = zeroval
    tln  = zeroval
    qln  = zeroval
    qccln  = zeroval
    qiiln  = zeroval
    uln  = zeroval
    vln  = zeroval
#if defined(SP_ESMT)
    uln_esmt = zeroval
    vln_esmt = zeroval
#endif
    cwp = zeroval
    cwph = zeroval
    cwpm = zeroval
    cwpl = zeroval
    flag_top = .false.
    gcolindex = zeroval
    cltemp = zeroval
    cmtemp = zeroval
    chtemp = zeroval
    cttemp = zeroval
#ifdef CLUBB_CRM
    rtm_integral_before  = zeroval
    rtm_integral_after  = zeroval
    thlm_integral_before = zeroval
    thlm_integral_after = zeroval
    thlm_before = zeroval
    thlm_after = zeroval
    rtm_column = zeroval
#endif /* CLUBB_CRM */

  call allocate_grid()
  call allocate_tracers()
  call allocate_sgs()
  call allocate_vars()
#ifdef sam1mom
  call allocate_micro_params()
  call allocate_micro()
#endif
#if defined(SP_ESMT)
  call allocate_scalar_momentum()
#endif

  !Loop over "vector columns"
  do icrm = 1 , ncrms


    !MRN: In standalone mode, we need to pass these things in by parameter, not look them up.
    latitude0  = get_rlat_p(lchnk, icol(icrm)) * 57.296_r8
    longitude0 = get_rlon_p(lchnk, icol(icrm)) * 57.296_r8

    igstep = get_nstep()

!-----------------------------------------------

    dostatis  = .false.    ! no statistics are collected.
    idt_gl    = 1._r8/dt_gl
    ptop      = plev-nzm+1
    factor_xy = 1._r8/dble(nx*ny)
    dummy     = 0.
    crm_rad%temperature  (icrm,:,:,:) = 0.
    crm_rad%qv (icrm,:,:,:) = 0.
    crm_rad%qc (icrm,:,:,:) = 0.
    crm_rad%qi (icrm,:,:,:) = 0.
    crm_rad%cld(icrm,:,:,:) = 0.
#ifdef m2005
    crm_rad%nc(icrm,:,:,:) = 0.0
    crm_rad%ni(icrm,:,:,:) = 0.0
    crm_rad%qs(icrm,:,:,:) = 0.0
    crm_rad%ns(icrm,:,:,:) = 0.0
#endif /* m2005 */
    zs=crm_input%phis(icrm)/ggr
    bflx = crm_input%bflxls(icrm)
    wnd = crm_input%wndls(icrm)

!-----------------------------------------

#ifdef CLUBB_CRM
    if(igstep == 1) then
      lrestart_clubb = .false.
    else
     lrestart_clubb = .true.
    endif
#endif /* CLUBB_CRM */

    call task_init ()
    call setparm()

    if(fcor.eq.-999.) fcor= 4*pi/86400.*sin(latitude0*pi/180.)
    fcorz = sqrt(4.*(2*pi/(3600.*24.))**2-fcor**2)
    fcory(:) = fcor
    fcorzy(:) = fcorz
    do j=1,ny
      do i=1,nx
        latitude (i,j) = latitude0
        longitude(i,j) = longitude0
      end do
    end do

    if(crm_input%ocnfrac(icrm).gt.0.5) then
       OCEAN = .true.
    else
       LAND = .true.
    end if

    ! Create CRM vertical grid and initialize some vertical reference arrays:
    do k = 1, nzm
      z(k) = crm_input%zmid(icrm,plev-k+1) - crm_input%zint(icrm,plev+1)
      zi(k) = crm_input%zint(icrm,plev-k+2)- crm_input%zint(icrm,plev+1)
      pres(k) = crm_input%pmid(icrm,plev-k+1)/100.
      presi(k) = crm_input%pint(icrm,plev-k+2)/100.
      prespot(k)=(1000./pres(k))**(rgas/cp)
      bet(k) = ggr/crm_input%tl(icrm,plev-k+1)
      gamaz(k)=ggr/cp*z(k)
    end do ! k
   ! zi(nz) =  crm_input%zint(plev-nz+2)
    zi(nz) = crm_input%zint(icrm,plev-nz+2)-crm_input%zint(icrm,plev+1) !+++mhwang, 2012-02-04
    presi(nz) = crm_input%pint(icrm, plev-nz+2)/100.

    dz = 0.5*(z(1)+z(2))
    do k=2,nzm
      adzw(k) = (z(k)-z(k-1))/dz
    end do
    adzw(1)  = 1.
    adzw(nz) = adzw(nzm)
    !+++mhwang fix the adz bug. (adz needs to be consistent with zi)
    !2012-02-04 Minghuai Wang (minghuai.wang@pnnl.gov)
    do k=1, nzm
      adz(k)=(zi(k+1)-zi(k))/dz
    end do

    do k = 1,nzm
      rho(k) = crm_input%pdel(icrm,plev-k+1)/ggr/(adz(k)*dz)
    end do
    do k=2,nzm
    ! rhow(k) = 0.5*(rho(k)+rho(k-1))
    !+++mhwang fix the rhow bug (rhow needes to be consistent with crm_input%pmid)
    !2012-02-04 Minghuai Wang (minghuai.wang@pnnl.gov)
      rhow(k) = (crm_input%pmid(icrm,plev-k+2)-crm_input%pmid(icrm,plev-k+1))/ggr/(adzw(k)*dz)
    end do
    rhow(1) = 2.*rhow(2) - rhow(3)
#ifdef CLUBB_CRM /* Fix extrapolation for 30 point grid */
    if (  2.*rhow(nzm) - rhow(nzm-1) > 0. ) then
       rhow(nz)= 2.*rhow(nzm) - rhow(nzm-1)
    else
       rhow(nz)= sqrt( rhow(nzm) )
    endif
#else
    rhow(nz)= 2.*rhow(nzm) - rhow(nzm-1)
#endif /*CLUBB_CRM*/
    colprec=0
    colprecs=0

    !  Initialize CRM fields:
    u   (1:nx,1:ny,1:nzm) = crm_state%u_wind(icrm,1:nx,1:ny,1:nzm)
    v   (1:nx,1:ny,1:nzm) = crm_state%v_wind(icrm,1:nx,1:ny,1:nzm)*YES3D
    w   (1:nx,1:ny,1:nzm) = crm_state%w_wind(icrm,1:nx,1:ny,1:nzm)
    tabs(1:nx,1:ny,1:nzm) = crm_state%temperature(icrm,1:nx,1:ny,1:nzm)

    ! limit the velocity at the very first step:
    if(u(1,1,1).eq.u(2,1,1).and.u(3,1,2).eq.u(4,1,2)) then
      do k=1,nzm
        do j=1,ny
          do i=1,nx
            u(i,j,k) = min( umax, max(-umax,u(i,j,k)) )
            v(i,j,k) = min( umax, max(-umax,v(i,j,k)) )*YES3D
          enddo
        enddo
      enddo
    endif


#if defined(SP_ESMT)
    do k=1,nzm
      u_esmt(:,:,k) = crm_input%ul_esmt(icrm,plev-k+1)
      v_esmt(:,:,k) = crm_input%vl_esmt(icrm,plev-k+1)
    end do
#endif

      ! Populate microphysics array from crm_state
#ifdef m2005
      micro_field(1:nx,1:ny,1:nzm,1)  = crm_state%qt(icrm,1:nx,1:ny,1:nzm)
      micro_field(1:nx,1:ny,1:nzm,2)  = crm_state%nc(icrm,1:nx,1:ny,1:nzm)
      micro_field(1:nx,1:ny,1:nzm,3)  = crm_state%qr(icrm,1:nx,1:ny,1:nzm)
      micro_field(1:nx,1:ny,1:nzm,4)  = crm_state%nr(icrm,1:nx,1:ny,1:nzm)
      micro_field(1:nx,1:ny,1:nzm,5)  = crm_state%qi(icrm,1:nx,1:ny,1:nzm)
      micro_field(1:nx,1:ny,1:nzm,6)  = crm_state%ni(icrm,1:nx,1:ny,1:nzm)
      micro_field(1:nx,1:ny,1:nzm,7)  = crm_state%qs(icrm,1:nx,1:ny,1:nzm)
      micro_field(1:nx,1:ny,1:nzm,8)  = crm_state%ns(icrm,1:nx,1:ny,1:nzm)
      micro_field(1:nx,1:ny,1:nzm,9)  = crm_state%qg(icrm,1:nx,1:ny,1:nzm)
      micro_field(1:nx,1:ny,1:nzm,10) = crm_state%ng(icrm,1:nx,1:ny,1:nzm)
      cloudliq(1:nx,1:ny,1:nzm) = crm_state%qc(icrm,1:nx,1:ny,1:nzm)
#else
      micro_field(1:nx,1:ny,1:nzm,1) = crm_state%qt(icrm,1:nx,1:ny,1:nzm)
      micro_field(1:nx,1:ny,1:nzm,2) = crm_state%qp(icrm,1:nx,1:ny,1:nzm)
      qn(1:nx,1:ny,1:nzm) = crm_state%qn(icrm,1:nx,1:ny,1:nzm)
#endif

#ifdef m2005
    do k=1, nzm
#ifdef MODAL_AERO
      ! set aerosol data
      l=plev-k+1
      naer (k, 1:ntot_amode) = crm_input%naermod (icrm,l, 1:ntot_amode)
      vaer (k, 1:ntot_amode) = crm_input%vaerosol(icrm,l, 1:ntot_amode)
      hgaer(k, 1:ntot_amode) = crm_input%hygro   (icrm,l, 1:ntot_amode)
#endif /* MODAL_AERO */
      do j=1, ny
        do i=1, nx
          if(cloudliq(i,j,k).gt.0) then
            if(dopredictNc) then
              if( micro_field(i,j,k,incl).eq.0) micro_field(i,j,k,incl) = 1.0e6*Nc0/rho(k)
            endif
          endif
        enddo
      enddo
    enddo
#endif /* m2005 */

    w(:,:,nz)=0.
    wsub (:) = 0.      !used in clubb, +++mhwang
    dudt(1:nx,1:ny,1:nzm,1:3) = 0.
    dvdt(1:nx,1:ny,1:nzm,1:3) = 0.
    dwdt(1:nx,1:ny,1:nz,1:3) = 0.
    tke (1:nx,1:ny,1:nzm) = 0.
    tk  (1:nx,1:ny,1:nzm) = 0.
    tkh (1:nx,1:ny,1:nzm) = 0.
    p   (1:nx,1:ny,1:nzm) = 0.

    CF3D(1:nx,1:ny,1:nzm) = 1.

    call micro_init()

    ! initialize sgs fields
    call sgs_init()

    do k=1,nzm
      u0(k)=0.
      v0(k)=0.
      t0(k)=0.
      t00(k)=0.
      tabs0(k)=0.
      q0(k)=0.
      qv0(k)=0.
      !+++mhwang these are not initialized ??
      qn0(k) = 0.0
      qp0(k) = 0.0
      tke0(k) = 0.0
      !---mhwang
      do j=1,ny
        do i=1,nx
          t(i,j,k) = tabs(i,j,k)+gamaz(k) &
                    -fac_cond*qcl(i,j,k)-fac_sub*qci(i,j,k) &
                    -fac_cond*qpl(i,j,k)-fac_sub*qpi(i,j,k)
          colprec=colprec+(qpl(i,j,k)+qpi(i,j,k))*crm_input%pdel(icrm,plev-k+1)
          colprecs=colprecs+qpi(i,j,k)*crm_input%pdel(icrm,plev-k+1)
          u0(k)=u0(k)+u(i,j,k)
          v0(k)=v0(k)+v(i,j,k)
          t0(k)=t0(k)+t(i,j,k)
          t00(k)=t00(k)+t(i,j,k)+fac_cond*qpl(i,j,k)+fac_sub*qpi(i,j,k)
          tabs0(k)=tabs0(k)+tabs(i,j,k)
          q0(k)=q0(k)+qv(i,j,k)+qcl(i,j,k)+qci(i,j,k)
          qv0(k) = qv0(k) + qv(i,j,k)
          qn0(k) = qn0(k) + qcl(i,j,k) + qci(i,j,k)
          qp0(k) = qp0(k) + qpl(i,j,k) + qpi(i,j,k)
          tke0(k)=tke0(k)+tke(i,j,k)
        enddo
      enddo

      u0(k) = u0(k) * factor_xy
      v0(k) = v0(k) * factor_xy
      t0(k) = t0(k) * factor_xy
      t00(k) = t00(k) * factor_xy
      tabs0(k) = tabs0(k) * factor_xy
      q0(k) = q0(k) * factor_xy
      qv0(k) = qv0(k) * factor_xy
      qn0(k) = qn0(k) * factor_xy
      qp0(k) = qp0(k) * factor_xy
      tke0(k) = tke0(k) * factor_xy
#ifdef CLUBB_CRM
      ! Update thetav for CLUBB.  This is needed when we have a higher model top
      ! than is in the sounding, because we subsequently use tv0 to initialize
      ! thv_ds_zt/zm, which appear in CLUBB's anelastic buoyancy terms.
      ! -dschanen UWM 11 Feb 2010
      tv0(k) = tabs0(k)*prespot(k)*(1.+epsv*q0(k))
#endif /* CLUBB_CRM */

      l = plev-k+1
      uln(l) = min( umax, max(-umax,crm_input%ul(icrm,l)) )
      vln(l) = min( umax, max(-umax,crm_input%vl(icrm,l)) )*YES3D
      ttend(k) = (crm_input%tl(icrm,l)+gamaz(k)- fac_cond*(crm_input%qccl(icrm,l)+crm_input%qiil(icrm,l))-fac_fus*crm_input%qiil(icrm,l)-t00(k))*idt_gl
      qtend(k) = (crm_input%ql(icrm,l)+crm_input%qccl(icrm,l)+crm_input%qiil(icrm,l)-q0(k))*idt_gl
      utend(k) = (uln(l)-u0(k))*idt_gl
      vtend(k) = (vln(l)-v0(k))*idt_gl
      ug0(k) = uln(l)
      vg0(k) = vln(l)
      tg0(k) = crm_input%tl(icrm,l)+gamaz(k)-fac_cond*crm_input%qccl(icrm,l)-fac_sub*crm_input%qiil(icrm,l)
      qg0(k) = crm_input%ql(icrm,l)+crm_input%qccl(icrm,l)+crm_input%qiil(icrm,l)

    end do ! k

    uhl = u0(1)
    vhl = v0(1)

! estimate roughness length assuming logarithmic profile of velocity near the surface:

    ustar = sqrt(crm_input%tau00(icrm)/rho(1))
    z0 = z0_est(z(1),bflx,wnd,ustar)
    z0 = max(real(0.00001,crm_rknd),min(real(1.,crm_rknd),z0))

    crm_output%timing_factor = 0.

    crm_output%prectend(icrm)=colprec
    crm_output%precstend(icrm)=colprecs


#ifdef CLUBB_CRM
    if(doclubb) then
      fluxbu(:, :) = crm_input%fluxu00(icrm)/rhow(1)
      fluxbv(:, :) = crm_input%fluxv00(icrm)/rhow(1)
      fluxbt(:, :) = crm_input%fluxt00(icrm)/rhow(1)
      fluxbq(:, :) = crm_input%fluxq00(icrm)/rhow(1)
    else
      fluxbu(:, :) = 0.
      fluxbv(:, :) = 0.
      fluxbt(:, :) = 0.
      fluxbq(:, :) = 0.
    endif
#else
    fluxbu=0.
    fluxbv=0.
    fluxbt=0.
    fluxbq=0.
#endif /* CLUBB_CRM */
    fluxtu=0.
    fluxtv=0.
    fluxtt=0.
    fluxtq=0.
    fzero =0.
    precsfc=0.
    precssfc=0.

!---------------------------------------------------
    crm_output%cld   (icrm,:) = 0.
    crm_output%cldtop(icrm,:) = 0.
    crm_output%gicewp(icrm,:) = 0
    crm_output%gliqwp(icrm,:) = 0
    crm_output%mctot (icrm,:) = 0.
    crm_output%mcup  (icrm,:) = 0.
    crm_output%mcdn  (icrm,:) = 0.
    crm_output%mcuup (icrm,:) = 0.
    crm_output%mcudn (icrm,:) = 0.
    crm_output%qc_mean(icrm,:) = 0.
    crm_output%qi_mean(icrm,:) = 0.
    crm_output%qs_mean(icrm,:) = 0.
    crm_output%qg_mean(icrm,:) = 0.
    crm_output%qr_mean(icrm,:) = 0.
#ifdef m2005
    crm_output%nc_mean(icrm,:) = 0.
    crm_output%ni_mean(icrm,:) = 0.
    crm_output%ns_mean(icrm,:) = 0.
    crm_output%ng_mean(icrm,:) = 0.
    crm_output%nr_mean(icrm,:) = 0.
    ! hm 8/31/11 add new variables
    crm_output%aut_a (icrm,:) = 0.
    crm_output%acc_a (icrm,:) = 0.
    crm_output%evpc_a(icrm,:) = 0.
    crm_output%evpr_a(icrm,:) = 0.
    crm_output%mlt_a (icrm,:) = 0.
    crm_output%sub_a (icrm,:) = 0.
    crm_output%dep_a (icrm,:) = 0.
    crm_output%con_a (icrm,:) = 0.

    ! hm 8/31/11 add new output
    ! these are increments added to calculate gcm-grid and time-step avg
    ! note - these values are also averaged over the icycle loop following
    ! the approach for precsfc
    aut1a  = 0.
    acc1a  = 0.
    evpc1a = 0.
    evpr1a = 0.
    mlt1a  = 0.
    sub1a  = 0.
    dep1a  = 0.
    con1a  = 0.
#endif /* m2005 */

    crm_output%mu_crm (icrm,:) = 0.
    crm_output%md_crm (icrm,:) = 0.
    crm_output%eu_crm (icrm,:) = 0.
    crm_output%du_crm (icrm,:) = 0.
    crm_output%ed_crm (icrm,:) = 0.
    dd_crm (icrm,:) = 0.
    crm_output%jt_crm (icrm)   = 0.
    crm_output%mx_crm (icrm)   = 0.

    mui_crm(icrm,:) = 0.
    mdi_crm(icrm,:) = 0.

    crm_output%flux_qt   (icrm,:) = 0.
    crm_output%flux_u    (icrm,:) = 0.
    crm_output%flux_v    (icrm,:) = 0.
    crm_output%fluxsgs_qt(icrm,:) = 0.
    crm_output%tkez      (icrm,:) = 0.
    crm_output%tkesgsz   (icrm,:) = 0.
    crm_output%tkz       (icrm,:) = 0.
    crm_output%flux_qp   (icrm,:) = 0.
    crm_output%precflux      (icrm,:) = 0.
    crm_output%qt_trans  (icrm,:) = 0.
    crm_output%qp_trans  (icrm,:) = 0.
    crm_output%qp_fall   (icrm,:) = 0.
    crm_output%qp_evp    (icrm,:) = 0.
    crm_output%qp_src    (icrm,:) = 0.
    crm_output%qt_ls     (icrm,:) = 0.
    crm_output%t_ls      (icrm,:) = 0.

    uwle     = 0.
    uwsb     = 0.
    vwle     = 0.
    vwsb     = 0.
    qpsrc    = 0.
    qpevp    = 0.
    qpfall   = 0.
    precflux = 0.

!--------------------------------------------------
#ifdef sam1mom
    if(doprecip) call precip_init()
#endif

    !MRN: Don't want any stochasticity introduced in the standalone.
    !MRN: Need to make sure the first call to crm(...) is not dumped out
    !MRN: Also want to avoid the rabbit hole of dependencies eminating from get_gcol_all_p in phys_grid!


#ifndef CRM_STANDALONE
    if ( igstep <= 1 ) then
        iseed = get_gcol_p(lchnk,icol(icrm)) * perturb_seed_scale 
        call setperturb(iseed)
    end if
#endif

    !--------------------------
    ! whannah - sanity check for new method to calculate radiation
    ! over averaged groups of columns instead of each individually
    if ( mod(nx,crm_nx_rad)==0 .or. mod(nx,crm_nx_rad)==0  ) then
      crm_nx_rad_fac = real(crm_nx_rad,crm_rknd)/real(nx,crm_rknd)
      crm_ny_rad_fac = real(crm_ny_rad,crm_rknd)/real(ny,crm_rknd)
    else
      write(0,*) "crm_nx_rad and crm_ny_rad need to be divisible by nx and ny"
      call endrun('crm main')
    end if

#ifndef CLUBB_CRM
    !--------------------------
    ! do a CLUBB sanity check
    if ( doclubb .or. doclubbnoninter ) then
      write(0,*) "Cannot call CLUBB if -DCLUBB is not in FFLAGS"
      call endrun('crm main')
    endif
#endif
#ifdef CLUBB_CRM
    !------------------------------------------------------------------
    ! Do initialization for UWM CLUBB
    !------------------------------------------------------------------
    up2       (1:nx, 1:ny, 1:nz ) = clubb_buffer(icrm,1:nx, 1:ny, 1:nz ,  1)
    vp2       (1:nx, 1:ny, 1:nz ) = clubb_buffer(icrm,1:nx, 1:ny, 1:nz ,  2)
    wprtp     (1:nx, 1:ny, 1:nz ) = clubb_buffer(icrm,1:nx, 1:ny, 1:nz ,  3)
    wpthlp    (1:nx, 1:ny, 1:nz ) = clubb_buffer(icrm,1:nx, 1:ny, 1:nz ,  4)
    wp2       (1:nx, 1:ny, 1:nz ) = clubb_buffer(icrm,1:nx, 1:ny, 1:nz ,  5)
    wp3       (1:nx, 1:ny, 1:nz ) = clubb_buffer(icrm,1:nx, 1:ny, 1:nz ,  6)
    rtp2      (1:nx, 1:ny, 1:nz ) = clubb_buffer(icrm,1:nx, 1:ny, 1:nz ,  7)
    thlp2     (1:nx, 1:ny, 1:nz ) = clubb_buffer(icrm,1:nx, 1:ny, 1:nz ,  8)
    rtpthlp   (1:nx, 1:ny, 1:nz ) = clubb_buffer(icrm,1:nx, 1:ny, 1:nz ,  9)
    upwp      (1:nx, 1:ny, 1:nz ) = clubb_buffer(icrm,1:nx, 1:ny, 1:nz , 10)
    vpwp      (1:nx, 1:ny, 1:nz ) = clubb_buffer(icrm,1:nx, 1:ny, 1:nz , 11)
    cloud_frac(1:nx, 1:ny, 1:nz ) = clubb_buffer(icrm,1:nx, 1:ny, 1:nz , 12)
    t_tndcy   (1:nx, 1:ny, 1:nzm) = clubb_buffer(icrm,1:nx, 1:ny, 1:nzm, 13)
    qc_tndcy  (1:nx, 1:ny, 1:nzm) = clubb_buffer(icrm,1:nx, 1:ny, 1:nzm, 14)
    qv_tndcy  (1:nx, 1:ny, 1:nzm) = clubb_buffer(icrm,1:nx, 1:ny, 1:nzm, 15)
    u_tndcy   (1:nx, 1:ny, 1:nzm) = clubb_buffer(icrm,1:nx, 1:ny, 1:nzm, 16)
    v_tndcy   (1:nx, 1:ny, 1:nzm) = clubb_buffer(icrm,1:nx, 1:ny, 1:nzm, 17)

    ! since no tracer is carried in the current version of MMF, these
    ! tracer-related restart varialbes are set to zero. +++mhwang, 2011-08
    tracer_tndcy = 0.0
    sclrp2       = 0.0
    sclrprtp     = 0.0
    sclrpthlp    = 0.0
    wpsclrp      = 0.0

    if((doclubb.and.docloud).or.(.not.doclubb .and. .not.docloud)) then
      write(0, *) 'doclubb and docloud can not both be true or be false'
      call endrun('crm_clubb2')
    endif
    if((doclubb_sfc_fluxes.and.docam_sfc_fluxes)) then
      write(0, *) 'doclubb_sfc_fluxes and dosam_sfc_fluxes can not both be true'
      call endrun('crm_clubb_fluxes')
    endif

    if ( doclubb .or. doclubbnoninter ) then
      call clubb_sgs_setup( real( dt*real( nclubb ), kind=time_precision), &
                            latitude, longitude, z, rho, zi, rhow, tv0, tke )
    endif
#endif /* CLUBB_CRM */

#ifdef ECPP
    !ntavg1_ss = dt_gl/3   ! one third of GCM time step, 10 minutes
    ntavg1_ss = min(600._r8, dt_gl)       ! 10 minutes  or the GCM timestep, whichever smaller
          ! ntavg1_ss = number of seconds to average between computing categories.
    ntavg2_ss = dt_gl   ! GCM time step
          ! ntavg2_ss = number of seconds to average between outputs.
          !    This must be a multiple of ntavgt1_ss.

    ! ecpp_crm_init has to be called after ntavg1_ss and ntavg2_ss
    ! are set for their values are used in ecpp_crm_init.
    call ecpp_crm_init()

    qlsink    = 0.0
    qlsink_bf = 0.0
    prain     = 0.0
    precr     = 0.0
    precsolid = 0.0
#endif /* ECPP */

     qtotmicro(:) = 0.0
!    !+++mhwangtest
!    ! test water conservtion problem
!    ntotal_step = 0.0
!    qtot(icrm,:) = 0.0
!    do k=1, nzm
!      l=plev-k+1
!      do j=1, ny
!        do i=1, nx
!#ifdef m2005
!          qtot(icrm,1) = qtot(icrm,1)+((micro_field(i,j,k,iqr)+micro_field(i,j,k,iqs)+micro_field(i,j,k,iqg)) * crm_input%pdel(icrm,l)/ggr)/(nx*ny)
!#endif
!#ifdef sam1mom
!          qtot(icrm,1) = qtot(icrm,1)+(qpl(i,j,k)+qpi(i,j,k)) * crm_input%pdel(icrm,l)/ggr/(nx*ny)
!#endif
!        enddo
!      enddo
!      qtot(icrm,1) = qtot(icrm,1) + (crm_input%ql(icrm,l)+crm_input%qccl(icrm,l)+crm_input%qiil(icrm,l)) * crm_input%pdel(icrm,l)/ggr
!    enddo
!    !---mhwangtest


    nstop = dt_gl/dt
    dt = dt_gl/nstop
    nsave3D = nint(60/dt)
    nstep  = 0
    nprint = 1
    ncycle = 0
    day=day0

#if defined( SP_CRM_SPLIT )
    nstop  = ceiling( nstop * 0.5 )
    crm_run_time  = dt_gl * 0.5
    icrm_run_time = 1._r8/crm_run_time
#else
    crm_run_time  = dt_gl
    icrm_run_time = 1._r8/crm_run_time
#endif

    factor_xyt = factor_xy / real(nstop,crm_rknd)

    !========================================================================================
    !----------------------------------------------------------------------------------------
    !   Main time loop
    !----------------------------------------------------------------------------------------
    !========================================================================================

    do while (nstep.lt.nstop)
      nstep = nstep + 1
      time = time + dt
      day = day0 + time/86400.
      crm_output%timing_factor(icrm) = crm_output%timing_factor(icrm)+1
      !------------------------------------------------------------------
      !  Check if the dynamical time step should be decreased
      !  to handle the cases when the flow being locally linearly unstable
      !------------------------------------------------------------------
      ncycle = 1

      call kurant()

      do icyc=1,ncycle

        icycle = icyc
        dtn = dt/ncycle
        dt3(na) = dtn
        dtfactor = dtn/dt

        !---------------------------------------------
        !  	the Adams-Bashforth scheme in time
        call abcoefs()

        !---------------------------------------------
        !  	initialize stuff:
        call zero()

        !-----------------------------------------------------------
        !       Buoyancy term:
        call buoyancy()

        !+++mhwangtest
        ! test water conservtion problem
        !ntotal_step = ntotal_step + 1.
        !---mhwangtest

        !------------------------------------------------------------
        !       Large-scale and surface forcing:
        call forcing()


        !!! Apply radiative tendency
        do k=1,nzm
          do j=1,ny
            do i=1,nx
              i_rad = ceiling( real(i,crm_rknd) * crm_nx_rad_fac )
              j_rad = ceiling( real(j,crm_rknd) * crm_ny_rad_fac )
              t(i,j,k) = t(i,j,k) + crm_rad%qrad(icrm,i_rad,j_rad,k)*dtn
            enddo
          enddo
        enddo

        !----------------------------------------------------------
        !   	suppress turbulence near the upper boundary (spange):
        if (dodamping) call damping()

        !---------------------------------------------------------
        !   Ice fall-out

#ifdef CLUBB_CRM
        if ( docloud .or. doclubb ) then
          call ice_fall()
        endif
#else
        if(docloud) then
            call ice_fall()
        endif
#endif

        !----------------------------------------------------------
        !     Update scalar boundaries after large-scale processes:
        call boundaries(3)

        !---------------------------------------------------------
        !     Update boundaries for velocities:
        call boundaries(0)

        !-----------------------------------------------
        !     surface fluxes:
        if (dosurface) call crmsurface(bflx)

        !-----------------------------------------------------------
        !  SGS physics:
        if (dosgs) call sgs_proc()

#ifdef CLUBB_CRM_OLD
        !----------------------------------------------------------
        ! Do a timestep with CLUBB if enabled:
        ! -dschanen UWM 16 May 2008

        if ( doclubb .or. doclubbnoninter ) then
          ! In case of ice fall, we recompute qci here for the
          ! single-moment scheme.  Also, subsidence, diffusion and advection have
          ! been applied to micro_field but not qv/qcl so they must be updated.
          call micro_update()
        endif ! doclubb .or. doclubbnoninter

        if ( doclubb ) then
          ! Calculate the vertical integrals for RTM and THLM so we can later
          ! calculate whether CLUBB is a spurious source or sink of either.
          ! - nielsenb UWM 4 Jun 2010
          do i = 1,nx
            do j = 1,ny
              rtm_column = qv(i,j,1:nzm) + qcl(i,j,1:nzm)
              rtm_integral_before(i,j) = vertical_integral( (nz - 2 + 1), rho_ds_zt(2:nz), &
                                           rtm_column, gr%invrs_dzt(2:nz) )

              thlm_before = t2thetal( t(i,j,1:nzm), gamaz(1:nzm), &
                                   qcl(i,j,1:nzm), qpl(i,j,1:nzm), &
                                   qci(i,j,1:nzm), qpi(i,j,1:nzm), &
                                   prespot(1:nzm) )

              thlm_integral_before(i,j) = vertical_integral( (nz - 2 + 1), rho_ds_zt(2:nz), &
                                                            thlm_before(1:nzm), gr%invrs_dzt(2:nz) )
            enddo
          enddo
          ! End vertical integral
        endif ! doclubb

        if ( doclubb .or. doclubbnoninter ) then
          ! We call CLUBB here because adjustments to the wind
          ! must occur prior to adams() -dschanen 26 Aug 2008
          ! Here we call clubb only if nstep divides the current timestep,
          ! or we're on the very first timestep
          if ( nstep == 1 .or. mod( nstep, nclubb ) == 0 ) then

            call advance_clubb_sgs( real( dtn*real( nclubb ), kind=time_precision), & ! in
                                    real( 0., kind=time_precision ),         & ! in
                                    real( time, kind=time_precision ),       & ! in
                                    rho, rhow, wsub, u, v, w, qpl, qci, qpi, & ! in
                                    t, qv, qcl ) ! in
          endif ! nstep == 1 .or. mod( nstep, nclubb) == 0
        endif ! doclubb .or. doclubbnoninter

#endif /* CLUBB_CRM_OLD */
        !----------------------------------------------------------
        !     Fill boundaries for SGS diagnostic fields:

        call boundaries(4)

        !-----------------------------------------------
        !       advection of momentum:
        call advect_mom()

        !----------------------------------------------------------
        !	SGS effects on momentum:

        if(dosgs) call sgs_mom()
#ifdef CLUBB_CRM_OLD
        if ( doclubb ) then
          !          call apply_clubb_sgs_tndcy_mom &
          !               ( dudt, dvdt ) ! in/out
        endif
#endif /*CLUBB_CRM_OLD*/

        !-----------------------------------------------------------
        !       Coriolis force:
        if (docoriolis) call coriolis()

        !---------------------------------------------------------
        !       compute rhs of the Poisson equation and solve it for pressure.
        call pressure()

        !---------------------------------------------------------
        !       find velocity field at n+1/2 timestep needed for advection of scalars:
        !  Note that at the end of the call, the velocities are in nondimensional form.
        call adams()

        !----------------------------------------------------------
        !     Update boundaries for all prognostic scalar fields for advection:
        call boundaries(2)

        !---------------------------------------------------------
        !      advection of scalars :
        call advect_all_scalars()

        !-----------------------------------------------------------
        !    Convert velocity back from nondimensional form:
        call uvw()

        !----------------------------------------------------------
        !     Update boundaries for scalars to prepare for SGS effects:
        call boundaries(3)

        !---------------------------------------------------------
        !      SGS effects on scalars :
        if (dosgs) call sgs_scalars()

#ifdef CLUBB_CRM_OLD
        ! Re-compute q/qv/qcl based on values computed in CLUBB
        if ( doclubb ) then
          ! Recalculate q, qv, qcl based on new micro_fields (updated by horizontal diffusion)
          call micro_update()

          ! Then Re-compute q/qv/qcl based on values computed in CLUBB
          call apply_clubb_sgs_tndcy_scalars( real( dtn, kind=time_precision), & ! in
                                              t, qv, qcl) ! in/out

          call micro_adjust( qv, qcl ) ! in

          ! Calculate the vertical integrals for RTM and THLM again so
          ! calculate whether CLUBB is a spurious source or sink of either.
          ! - nielsenb UWM 4 Jun 2010
          do i = 1,nx
            do j = 1,ny
              rtm_flux_top = rho_ds_zm(nz) * wprtp(i,j,nz)
              rtm_flux_sfc = rho_ds_zm(1) * fluxbq(i,j)
              rtm_column = qv(i,j,1:nzm) + qcl(i,j,1:nzm)
              rtm_integral_after(i,j) = vertical_integral( (nz - 2 + 1), rho_ds_zt(2:nz), &
                                            rtm_column, gr%invrs_dzt(2:nz) )

              rtm_spurious_source(i,j) = calculate_spurious_source( rtm_integral_after(i,j), &
                                                         rtm_integral_before(i,j), &
                                                         rtm_flux_top, rtm_flux_sfc, &
                                                         0.0_core_rknd, real( dtn, kind=core_rknd) )

              thlm_flux_top = rho_ds_zm(nz) * wpthlp(i,j,nz)
              thlm_flux_sfc = rho_ds_zm(1) * fluxbt(i,j)

              thlm_after = t2thetal( t(i,j,1:nzm), gamaz(1:nzm), &
                                     qcl(i,j,1:nzm), qpl(i,j,1:nzm), &
                                     qci(i,j,1:nzm), qpi(i,j,1:nzm), &
                                     prespot(1:nzm) )

              thlm_integral_after(i,j) = vertical_integral( (nz - 2 + 1), rho_ds_zt(2:nz), &
                                                         thlm_after(1:nzm), gr%invrs_dzt(2:nz))

              thlm_spurious_source(i,j) = calculate_spurious_source( thlm_integral_after(i,j), &
                                                             thlm_integral_before(i,j), &
                                                             thlm_flux_top, thlm_flux_sfc, &
                                                             0.0_core_rknd, real( dtn, kind=core_rknd ))
            enddo
          enddo
          ! End spurious source calculation
        endif  ! doclubb
#endif /*CLUBB_CRM_OLD*/

        !-----------------------------------------------------------
        !       Calculate PGF for scalar momentum tendency
#if defined( SP_ESMT ) && defined( SP_ESMT_PGF )
            call scalar_momentum_tend()
#endif

        !-----------------------------------------------------------
        !       Cloud condensation/evaporation and precipitation processes:
#ifdef CLUBB_CRM
        if(docloud.or.dosmoke.or.doclubb) call micro_proc()
#else
        if(docloud.or.dosmoke) call micro_proc()
#endif /*CLUBB_CRM*/

        !-----------------------------------------------------------
        !    Compute diagnostics fields:
          call diagnose()

        !----------------------------------------------------------
        ! Rotate the dynamic tendency arrays for Adams-bashforth scheme:
        nn=na
        na=nc
        nc=nb
        nb=nn
      enddo ! icycle

      !----------------------------------------------------------
      !----------------------------------------------------------
#ifdef ECPP
      ! Here ecpp_crm_stat is called every CRM time step (dt), not every subcycle time step (dtn).
      ! This is what the original MMF model did (crm_rad%temperature, crm_rad%qv, ...). Do we want to call ecpp_crm_stat
      ! every subcycle time step??? +++mhwang
      call ecpp_crm_stat()
#endif /*ECPP*/

      cwp  = 0.
      cwph = 0.
      cwpm = 0.
      cwpl = 0.

      flag_top(:,:) = .true.

      cltemp = 0.0; cmtemp = 0.0
      chtemp = 0.0; cttemp = 0.0

      do k=1,nzm
        l = plev-k+1
        do j=1,ny
          do i=1,nx
            ! hm modify 9/7/11 for end of timestep, GCM-grid scale hydrometeor output
            ! instead of time-step-averaged
            ! I also modified this for all q and N variables as well as for sam1mom
            ! for consistency
            !hm           crm_output%qc(l) = crm_output%qc(l) + qcl(i,j,k)
            !hm           crm_output%qi(l) = crm_output%qi(l) + qci(i,j,k)
            !hm           crm_output%qr(l) = crm_output%qr(l) + qpl(i,j,k)
            !hm#ifdef sam1mom
            !hm           omg = max(0.,min(1.,(tabs(i,j,k)-tgrmin)*a_gr))
            !hm           crm_output%qg(l) = crm_output%qg(l) + qpi(i,j,k)*omg
            !hm           crm_output%qs(l) = crm_output%qs(l) + qpi(i,j,k)*(1.-omg)
            !hm#else
            !           crm_output%qg(l) = crm_output%qg(l) + qpi(i,j,k)
            !           crm_output%qs(l) = crm_output%qs(l) + 0.     ! temporerary solution
            !hm           crm_output%qg(l) = crm_output%qg(l) + micro_field(i,j,k,iqg)
            !hm           crm_output%qs(l) = crm_output%qs(l) + micro_field(i,j,k,iqs)

            !hm           crm_output%nc(l) = crm_output%nc(l) + micro_field(i,j,k,incl)
            !hm           crm_output%ni(l) = crm_output%ni(l) + micro_field(i,j,k,inci)
            !hm           crm_output%nr(l) = crm_output%nr(l) + micro_field(i,j,k,inr)
            !hm           crm_output%ng(l) = crm_output%ng(l) + micro_field(i,j,k,ing)
            !hm           crm_output%ns(l) = crm_output%ns(l) + micro_field(i,j,k,ins)

            !hm#endif

            tmp1 = rho(nz-k)*adz(nz-k)*dz*(qcl(i,j,nz-k)+qci(i,j,nz-k))
            cwp(i,j) = cwp(i,j)+tmp1
            cttemp(i,j) = max(CF3D(i,j,nz-k), cttemp(i,j))
            if(cwp(i,j).gt.cwp_threshold.and.flag_top(i,j)) then
                crm_output%cldtop(icrm,l) = crm_output%cldtop(icrm,l) + 1
                flag_top(i,j) = .false.
            endif
            if(pres(nz-k).ge.700.) then
                cwpl(i,j) = cwpl(i,j)+tmp1
                cltemp(i,j) = max(CF3D(i,j,nz-k), cltemp(i,j))
            else if(pres(nz-k).lt.400.) then
                cwph(i,j) = cwph(i,j)+tmp1
                chtemp(i,j) = max(CF3D(i,j,nz-k), chtemp(i,j))
            else
                cwpm(i,j) = cwpm(i,j)+tmp1
                cmtemp(i,j) = max(CF3D(i,j,nz-k), cmtemp(i,j))
            endif

            !     qsat = qsatw_crm(tabs(i,j,k),pres(k))
            !     if(qcl(i,j,k)+qci(i,j,k).gt.min(1.e-5,0.01*qsat)) then
            tmp1 = rho(k)*adz(k)*dz
            if(tmp1*(qcl(i,j,k)+qci(i,j,k)).gt.cwp_threshold) then
                 crm_output%cld(icrm,l) = crm_output%cld(icrm,l) + CF3D(i,j,k)
                 if(w(i,j,k+1)+w(i,j,k).gt.2*wmin) then
                   crm_output%mcup (icrm,l) = crm_output%mcup (icrm,l) + rho(k)*0.5*(w(i,j,k+1)+w(i,j,k)) * CF3D(i,j,k)
                   crm_output%mcuup(icrm,l) = crm_output%mcuup(icrm,l) + rho(k)*0.5*(w(i,j,k+1)+w(i,j,k)) * (1.0 - CF3D(i,j,k))
                 endif
                 if(w(i,j,k+1)+w(i,j,k).lt.-2*wmin) then
                   crm_output%mcdn (icrm,l) = crm_output%mcdn (icrm,l) + rho(k)*0.5*(w(i,j,k+1)+w(i,j,k)) * CF3D(i,j,k)
                   crm_output%mcudn(icrm,l) = crm_output%mcudn(icrm,l) + rho(k)*0.5*(w(i,j,k+1)+w(i,j,k)) * (1. - CF3D(i,j,k))
                 endif
            else
                 if(w(i,j,k+1)+w(i,j,k).gt.2*wmin) then
                   crm_output%mcuup(icrm,l) = crm_output%mcuup(icrm,l) + rho(k)*0.5*(w(i,j,k+1)+w(i,j,k))
                 endif
                 if(w(i,j,k+1)+w(i,j,k).lt.-2*wmin) then
                   crm_output%mcudn(icrm,l) = crm_output%mcudn(icrm,l) + rho(k)*0.5*(w(i,j,k+1)+w(i,j,k))
                 endif
            endif

!             crm_rad%temperature  (icrm,i,j,k) = crm_rad%temperature  (icrm,i,j,k)+tabs(i,j,k)
!             crm_rad%qv (icrm,i,j,k) = crm_rad%qv (icrm,i,j,k)+max(real(0.,crm_rknd),qv(i,j,k))
!             crm_rad%qc (icrm,i,j,k) = crm_rad%qc (icrm,i,j,k)+qcl(i,j,k)
!             crm_rad%qi (icrm,i,j,k) = crm_rad%qi (icrm,i,j,k)+qci(i,j,k)
!             crm_rad%cld(icrm,i,j,k) = crm_rad%cld(icrm,i,j,k) +  CF3D(i,j,k)
! #ifdef m2005
!             crm_rad%nc(icrm,i,j,k) = crm_rad%nc(icrm,i,j,k)+micro_field(i,j,k,incl)
!             crm_rad%ni(icrm,i,j,k) = crm_rad%ni(icrm,i,j,k)+micro_field(i,j,k,inci)
!             crm_rad%qs(icrm,i,j,k) = crm_rad%qs(icrm,i,j,k)+micro_field(i,j,k,iqs)
!             crm_rad%ns(icrm,i,j,k) = crm_rad%ns(icrm,i,j,k)+micro_field(i,j,k,ins)
! #endif
          
            !!! only collect radiative inputs during tphysbc() when using SP_CRM_SPLIT
            if ( phys_stage == 1 ) then

              !!! Reduced radiation method allows for fewer radiation calculations
              !!! by collecting statistics and doing radiation over column groups
              i_rad = ceiling( real(i,crm_rknd) * crm_nx_rad_fac )
              j_rad = ceiling( real(j,crm_rknd) * crm_ny_rad_fac )

              crm_rad%temperature  (icrm,i_rad,j_rad,k) = crm_rad%temperature  (icrm,i_rad,j_rad,k) + tabs(i,j,k)
              crm_rad%qv (icrm,i_rad,j_rad,k) = crm_rad%qv (icrm,i_rad,j_rad,k) + max(real(0.,crm_rknd),qv(i,j,k))
              crm_rad%qc (icrm,i_rad,j_rad,k) = crm_rad%qc (icrm,i_rad,j_rad,k) + qcl(i,j,k)
              crm_rad%qi (icrm,i_rad,j_rad,k) = crm_rad%qi (icrm,i_rad,j_rad,k) + qci(i,j,k)
              crm_rad%cld(icrm,i_rad,j_rad,k) = crm_rad%cld(icrm,i_rad,j_rad,k) + CF3D(i,j,k)
#ifdef m2005
              crm_rad%nc(icrm,i_rad,j_rad,k) = crm_rad%nc(icrm,i_rad,j_rad,k) + micro_field(i,j,k,incl)
              crm_rad%ni(icrm,i_rad,j_rad,k) = crm_rad%ni(icrm,i_rad,j_rad,k) + micro_field(i,j,k,inci)
              crm_rad%qs(icrm,i_rad,j_rad,k) = crm_rad%qs(icrm,i_rad,j_rad,k) + micro_field(i,j,k,iqs)
              crm_rad%ns(icrm,i_rad,j_rad,k) = crm_rad%ns(icrm,i_rad,j_rad,k) + micro_field(i,j,k,ins)
#endif
            endif

            crm_output%gliqwp(icrm,l) = crm_output%gliqwp(icrm,l) + qcl(i,j,k)
            crm_output%gicewp(icrm,l) = crm_output%gicewp(icrm,l) + qci(i,j,k)
          enddo
        enddo
      enddo

      ! Diagnose mass fluxes to drive CAM's convective transport of tracers.
      ! definition of mass fluxes is taken from Xu et al., 2002, QJRMS.
      do k=1, nzm+1
        l=plev+1-k+1
        do j=1, ny
          do i=1, nx
            if(w(i,j,k).gt.0.) then
              kx=max(1, k-1)
              qsat = qsatw_crm(tabs(i,j,kx),pres(kx))
              if(qcl(i,j,kx)+qci(i,j,kx).gt.min(real(1.e-5,crm_rknd),0.01*qsat)) then
                mui_crm(icrm,l) = mui_crm(icrm,l)+rhow(k)*w(i,j,k)
              endif
            else if (w(i,j,k).lt.0.) then
              kx=min(k+1, nzm)
              qsat = qsatw_crm(tabs(i,j,kx),pres(kx))
              if(qcl(i,j,kx)+qci(i,j,kx).gt.min(real(1.e-5,crm_rknd),0.01*qsat)) then
                mdi_crm(icrm,l) = mdi_crm(icrm,l)+rhow(k)*w(i,j,k)
              else if(qpl(i,j,kx)+qpi(i,j,kx).gt.1.0e-4) then
                mdi_crm(icrm,l) = mdi_crm(icrm,l)+rhow(k)*w(i,j,k)
              endif
            endif
          enddo
        enddo
      enddo

      !do k=1,nzm
      ! radlwup0(k)=radlwup0(k)+radlwup(k)
      ! radlwdn0(k)=radlwdn0(k)+radlwdn(k)
      ! radqrlw0(k)=radqrlw0(k)+radqrlw(k)
      ! radswup0(k)=radswup0(k)+radswup(k)
      ! radswdn0(k)=radswdn0(k)+radswdn(k)
      ! radqrsw0(k)=radqrsw0(k)+radqrsw(k)
      !enddo

      do j=1,ny
        do i=1,nx
          !           if(cwp (i,j).gt.cwp_threshold) crm_output%cltot(icrm) = crm_output%cltot(icrm) + 1.
          !           if(cwph(i,j).gt.cwp_threshold) crm_output%clhgh(icrm) = crm_output%clhgh(icrm) + 1.
          !           if(cwpm(i,j).gt.cwp_threshold) crm_output%clmed(icrm) = crm_output%clmed(icrm) + 1.
          !           if(cwpl(i,j).gt.cwp_threshold) crm_output%cllow(icrm) = crm_output%cllow(icrm) + 1.
          !  use maxmimum cloud overlap to calcluate crm_output%cltot, crm_output%clhgh,
          !  cldmed, and cldlow   +++ mhwang
          if(cwp (i,j).gt.cwp_threshold) crm_output%cltot(icrm) = crm_output%cltot(icrm) + cttemp(i,j)
          if(cwph(i,j).gt.cwp_threshold) crm_output%clhgh(icrm) = crm_output%clhgh(icrm) + chtemp(i,j)
          if(cwpm(i,j).gt.cwp_threshold) crm_output%clmed(icrm) = crm_output%clmed(icrm) + cmtemp(i,j)
          if(cwpl(i,j).gt.cwp_threshold) crm_output%cllow(icrm) = crm_output%cllow(icrm) + cltemp(i,j)
        enddo
      enddo

      !        call stepout()
      !----------------------------------------------------------
    enddo ! main loop

    !========================================================================================
    !----------------------------------------------------------------------------------------
    ! End main time loop
    !----------------------------------------------------------------------------------------
    !========================================================================================

    !!! only collect radiative inputs during tphysbc()
    if ( phys_stage == 1 ) then

      tmp1 = crm_nx_rad_fac * crm_ny_rad_fac / real(nstop,crm_rknd)

      crm_rad%temperature  (icrm,:,:,:) = crm_rad%temperature  (icrm,:,:,:) * tmp1
      crm_rad%qv (icrm,:,:,:) = crm_rad%qv (icrm,:,:,:) * tmp1
      crm_rad%qc (icrm,:,:,:) = crm_rad%qc (icrm,:,:,:) * tmp1
      crm_rad%qi (icrm,:,:,:) = crm_rad%qi (icrm,:,:,:) * tmp1
      crm_rad%cld(icrm,:,:,:) = crm_rad%cld(icrm,:,:,:) * tmp1
#ifdef m2005
      crm_rad%nc(icrm,:,:,:) = crm_rad%nc(icrm,:,:,:) * tmp1
      crm_rad%ni(icrm,:,:,:) = crm_rad%ni(icrm,:,:,:) * tmp1
      crm_rad%qs(icrm,:,:,:) = crm_rad%qs(icrm,:,:,:) * tmp1
      crm_rad%ns(icrm,:,:,:) = crm_rad%ns(icrm,:,:,:) * tmp1
#endif /* m2005 */
    
    endif

    ! no CRM tendencies above its top
    tln  (1:ptop-1) =   crm_input%tl(icrm,1:ptop-1)
    qln  (1:ptop-1) =   crm_input%ql(icrm,1:ptop-1)
    qccln(1:ptop-1) = crm_input%qccl(icrm,1:ptop-1)
    qiiln(1:ptop-1) = crm_input%qiil(icrm,1:ptop-1)
    uln  (1:ptop-1) =   crm_input%ul(icrm,1:ptop-1)
    vln  (1:ptop-1) =   crm_input%vl(icrm,1:ptop-1)

    !  Compute tendencies due to CRM:
    tln  (ptop:plev) = 0.
    qln  (ptop:plev) = 0.
    qccln(ptop:plev) = 0.
    qiiln(ptop:plev) = 0.
    uln  (ptop:plev) = 0.
    vln  (ptop:plev) = 0.

#if defined( SP_ESMT )
    uln_esmt(1:ptop-1)  = crm_input%ul_esmt(icrm,1:ptop-1)
    vln_esmt(1:ptop-1)  = crm_input%vl_esmt(icrm,1:ptop-1)
    uln_esmt(ptop:plev) = 0.
    vln_esmt(ptop:plev) = 0.
#endif /* SP_ESMT */

    colprec=0
    colprecs=0
    do k = 1,nzm
      l = plev-k+1
      do i=1,nx
        do j=1,ny
          colprec = colprec +(qpl(i,j,k)+qpi(i,j,k))*crm_input%pdel(icrm,plev-k+1)
          colprecs= colprecs+qpi(i,j,k)*crm_input%pdel(icrm,plev-k+1)
          tln(l)  = tln(l)  +tabs(i,j,k)
          qln(l)  = qln(l)  +qv(i,j,k)
          qccln(l)= qccln(l)+qcl(i,j,k)
          qiiln(l)= qiiln(l)+qci(i,j,k)
          uln(l)  = uln(l)  +u(i,j,k)
          vln(l)  = vln(l)  +v(i,j,k)

#if defined(SP_ESMT)
          uln_esmt(l) = uln_esmt(l)+u_esmt(i,j,k)
          vln_esmt(l) = vln_esmt(l)+v_esmt(i,j,k)
#endif
        enddo ! j
      enddo ! i
    enddo ! k

    tln  (ptop:plev) = tln  (ptop:plev) * factor_xy
    qln  (ptop:plev) = qln  (ptop:plev) * factor_xy
    qccln(ptop:plev) = qccln(ptop:plev) * factor_xy
    qiiln(ptop:plev) = qiiln(ptop:plev) * factor_xy
    uln  (ptop:plev) = uln  (ptop:plev) * factor_xy
    vln  (ptop:plev) = vln  (ptop:plev) * factor_xy

#if defined(SP_ESMT)
    uln_esmt(ptop:plev) = uln_esmt(ptop:plev) * factor_xy
    vln_esmt(ptop:plev) = vln_esmt(ptop:plev) * factor_xy

    crm_output%ultend_esmt(icrm,:) = (uln_esmt(:) - crm_input%ul_esmt(icrm,:))*icrm_run_time
    crm_output%vltend_esmt(icrm,:) = (vln_esmt(:) - crm_input%vl_esmt(icrm,:))*icrm_run_time

    ! don't use tendencies from two top levels,
    crm_output%ultend_esmt(icrm,ptop:ptop+1) = 0.
    crm_output%vltend_esmt(icrm,ptop:ptop+1) = 0.
#endif

#if defined(SPMOMTRANS)
    !!! resolved convective momentum transport (CMT) tendencies
    crm_output%ultend(icrm,:) = (uln - crm_input%ul(icrm,:))*icrm_run_time
    crm_output%vltend(icrm,:) = (vln - crm_input%vl(icrm,:))*icrm_run_time

    !!! don't use tendencies from two top levels
    crm_output%ultend(icrm,ptop:ptop+1) = 0.
    crm_output%vltend(icrm,ptop:ptop+1) = 0.
#endif /* SPMOMTRANS */

    crm_output%sltend (icrm,:) = cp * (tln   - crm_input%tl  (icrm,:)) * icrm_run_time
    crm_output%qltend (icrm,:) =      (qln   - crm_input%ql  (icrm,:)) * icrm_run_time
    crm_output%qcltend(icrm,:) =      (qccln - crm_input%qccl(icrm,:)) * icrm_run_time
    crm_output%qiltend(icrm,:) =      (qiiln - crm_input%qiil(icrm,:)) * icrm_run_time
    crm_output%prectend (icrm) = (colprec -crm_output%prectend (icrm))/ggr*factor_xy * icrm_run_time
    crm_output%precstend(icrm) = (colprecs-crm_output%precstend(icrm))/ggr*factor_xy * icrm_run_time

    !!! don't use CRM tendencies from two crm top levels
    !!! radiation tendencies are added back after the CRM call (see crm_physics_tend)
    crm_output%sltend (icrm,ptop:ptop+1) = 0.
    crm_output%qltend (icrm,ptop:ptop+1) = 0.
    crm_output%qcltend(icrm,ptop:ptop+1) = 0.
    crm_output%qiltend(icrm,ptop:ptop+1) = 0.
    !-------------------------------------------------------------
    !
    ! Save the last step to the permanent core:
    crm_state%u_wind  (icrm,1:nx,1:ny,1:nzm) = u   (1:nx,1:ny,1:nzm)
    crm_state%v_wind  (icrm,1:nx,1:ny,1:nzm) = v   (1:nx,1:ny,1:nzm)
    crm_state%w_wind  (icrm,1:nx,1:ny,1:nzm) = w   (1:nx,1:ny,1:nzm)
    crm_state%temperature  (icrm,1:nx,1:ny,1:nzm) = tabs(1:nx,1:ny,1:nzm)

#ifdef m2005
      crm_state%qt(icrm,1:nx,1:ny,1:nzm) = micro_field(1:nx,1:ny,1:nzm,1)
      crm_state%nc(icrm,1:nx,1:ny,1:nzm) = micro_field(1:nx,1:ny,1:nzm,2)
      crm_state%qr(icrm,1:nx,1:ny,1:nzm) = micro_field(1:nx,1:ny,1:nzm,3)
      crm_state%nr(icrm,1:nx,1:ny,1:nzm) = micro_field(1:nx,1:ny,1:nzm,4)
      crm_state%qi(icrm,1:nx,1:ny,1:nzm) = micro_field(1:nx,1:ny,1:nzm,5)
      crm_state%ni(icrm,1:nx,1:ny,1:nzm) = micro_field(1:nx,1:ny,1:nzm,6)
      crm_state%qs(icrm,1:nx,1:ny,1:nzm) = micro_field(1:nx,1:ny,1:nzm,7)
      crm_state%ns(icrm,1:nx,1:ny,1:nzm) = micro_field(1:nx,1:ny,1:nzm,8)
      crm_state%qg(icrm,1:nx,1:ny,1:nzm) = micro_field(1:nx,1:ny,1:nzm,9)
      crm_state%ng(icrm,1:nx,1:ny,1:nzm) = micro_field(1:nx,1:ny,1:nzm,10)
      crm_state%qc(icrm,1:nx,1:ny,1:nzm) = cloudliq(1:nx,1:ny,1:nzm)
#else
      crm_state%qt(icrm,1:nx,1:ny,1:nzm) = micro_field(1:nx,1:ny,1:nzm,1)
      crm_state%qp(icrm,1:nx,1:ny,1:nzm) = micro_field(1:nx,1:ny,1:nzm,2)
      crm_state%qn(icrm,1:nx,1:ny,1:nzm) = qn(1:nx,1:ny,1:nzm)
#endif

    crm_output%tk   (icrm,1:nx,1:ny,1:nzm) = tk  (1:nx, 1:ny, 1:nzm)
    crm_output%tkh  (icrm,1:nx,1:ny,1:nzm) = tkh (1:nx, 1:ny, 1:nzm)
#ifdef CLUBB_CRM
    clubb_buffer(icrm,1:nx, 1:ny, 1:nz ,  1) = up2       (1:nx, 1:ny, 1:nz )
    clubb_buffer(icrm,1:nx, 1:ny, 1:nz ,  2) = vp2       (1:nx, 1:ny, 1:nz )
    clubb_buffer(icrm,1:nx, 1:ny, 1:nz ,  3) = wprtp     (1:nx, 1:ny, 1:nz )
    clubb_buffer(icrm,1:nx, 1:ny, 1:nz ,  4) = wpthlp    (1:nx, 1:ny, 1:nz )
    clubb_buffer(icrm,1:nx, 1:ny, 1:nz ,  5) = wp2       (1:nx, 1:ny, 1:nz )
    clubb_buffer(icrm,1:nx, 1:ny, 1:nz ,  6) = wp3       (1:nx, 1:ny, 1:nz )
    clubb_buffer(icrm,1:nx, 1:ny, 1:nz ,  7) = rtp2      (1:nx, 1:ny, 1:nz )
    clubb_buffer(icrm,1:nx, 1:ny, 1:nz ,  8) = thlp2     (1:nx, 1:ny, 1:nz )
    clubb_buffer(icrm,1:nx, 1:ny, 1:nz ,  9) = rtpthlp   (1:nx, 1:ny, 1:nz )
    clubb_buffer(icrm,1:nx, 1:ny, 1:nz , 10) = upwp      (1:nx, 1:ny, 1:nz )
    clubb_buffer(icrm,1:nx, 1:ny, 1:nz , 11) = vpwp      (1:nx, 1:ny, 1:nz )
    clubb_buffer(icrm,1:nx, 1:ny, 1:nz , 12) = cloud_frac(1:nx, 1:ny, 1:nz )
    clubb_buffer(icrm,1:nx, 1:ny, 1:nzm, 13) = t_tndcy   (1:nx, 1:ny, 1:nzm)
    clubb_buffer(icrm,1:nx, 1:ny, 1:nzm, 14) = qc_tndcy  (1:nx, 1:ny, 1:nzm)
    clubb_buffer(icrm,1:nx, 1:ny, 1:nzm, 15) = qv_tndcy  (1:nx, 1:ny, 1:nzm)
    clubb_buffer(icrm,1:nx, 1:ny, 1:nzm, 16) = u_tndcy   (1:nx, 1:ny, 1:nzm)
    clubb_buffer(icrm,1:nx, 1:ny, 1:nzm, 17) = v_tndcy   (1:nx, 1:ny, 1:nzm)

    crm_cld    (icrm,1:nx, 1:ny, 1:nz ) = cloud_frac  (1:nx, 1:ny, 1:nz )
    clubb_tk   (icrm,1:nx, 1:ny, 1:nzm) = tk_clubb    (1:nx, 1:ny, 1:nzm)
    clubb_tkh  (icrm,1:nx, 1:ny, 1:nzm) = tkh_clubb   (1:nx, 1:ny, 1:nzm)
    relvar     (icrm,1:nx, 1:ny, 1:nzm) = relvarg     (1:nx, 1:ny, 1:nzm)
    accre_enhan(icrm,1:nx, 1:ny, 1:nzm) = accre_enhang(1:nx, 1:ny, 1:nzm)
    qclvar     (icrm,1:nx, 1:ny, 1:nzm) = qclvarg     (1:nx, 1:ny, 1:nzm)
#endif /* CLUBB_CRM */

    do k=1,nzm
     do j=1,ny
      do i=1,nx
        crm_output%qcl(icrm,i,j,k) = qcl(i,j,k)
        crm_output%qci(icrm,i,j,k) = qci(i,j,k)
        crm_output%qpl(icrm,i,j,k) = qpl(i,j,k)
        crm_output%qpi(icrm,i,j,k) = qpi(i,j,k)
#ifdef m2005
        crm_output%wvar(icrm,i,j,k) = wvar (i,j,k)
        crm_output%aut (icrm,i,j,k) = aut1 (i,j,k)
        crm_output%acc (icrm,i,j,k) = acc1 (i,j,k)
        crm_output%evpc(icrm,i,j,k) = evpc1(i,j,k)
        crm_output%evpr(icrm,i,j,k) = evpr1(i,j,k)
        crm_output%mlt (icrm,i,j,k) = mlt1 (i,j,k)
        crm_output%sub (icrm,i,j,k) = sub1 (i,j,k)
        crm_output%dep (icrm,i,j,k) = dep1 (i,j,k)
        crm_output%con (icrm,i,j,k) = con1 (i,j,k)
#endif /* m2005 */
        enddo
      enddo
    enddo
    crm_output%z0m     (icrm) = z0
    crm_output%taux(icrm) = taux0 / dble(nstop)
    crm_output%tauy(icrm) = tauy0 / dble(nstop)

    !---------------------------------------------------------------
    !  Diagnostics:

    ! hm add 9/7/11, change from GCM-time step avg to end-of-timestep
    do k=1,nzm
      l = plev-k+1
      do j=1,ny
        do i=1,nx
          crm_output%qc_mean(icrm,l) = crm_output%qc_mean(icrm,l) + qcl(i,j,k)
          crm_output%qi_mean(icrm,l) = crm_output%qi_mean(icrm,l) + qci(i,j,k)
          crm_output%qr_mean(icrm,l) = crm_output%qr_mean(icrm,l) + qpl(i,j,k)
#ifdef sam1mom
          omg = max(real(0.,crm_rknd),min(real(1.,crm_rknd),(tabs(i,j,k)-tgrmin)*a_gr))
          crm_output%qg_mean(icrm,l) = crm_output%qg_mean(icrm,l) + qpi(i,j,k)*omg
          crm_output%qs_mean(icrm,l) = crm_output%qs_mean(icrm,l) + qpi(i,j,k)*(1.-omg)
#else
          !crm_output%qg_mean(icrm,l) = crm_output%qg_mean(icrm,l) + qpi(i,j,k)
          !crm_output%qs_mean(icrm,l) = crm_output%qs_mean(icrm,l) + 0.     ! temporerary solution
          crm_output%qg_mean(icrm,l) = crm_output%qg_mean(icrm,l) + micro_field(i,j,k,iqg)
          crm_output%qs_mean(icrm,l) = crm_output%qs_mean(icrm,l) + micro_field(i,j,k,iqs)

          crm_output%nc_mean(icrm,l) = crm_output%nc_mean(icrm,l) + micro_field(i,j,k,incl)
          crm_output%ni_mean(icrm,l) = crm_output%ni_mean(icrm,l) + micro_field(i,j,k,inci)
          crm_output%nr_mean(icrm,l) = crm_output%nr_mean(icrm,l) + micro_field(i,j,k,inr)
          crm_output%ng_mean(icrm,l) = crm_output%ng_mean(icrm,l) + micro_field(i,j,k,ing)
          crm_output%ns_mean(icrm,l) = crm_output%ns_mean(icrm,l) + micro_field(i,j,k,ins)
#endif /* sam1mom */
        enddo
      enddo
    enddo

    crm_output%cld   (icrm,:) = min( 1._r8, crm_output%cld   (icrm,:) * factor_xyt )
    crm_output%cldtop(icrm,:) = min( 1._r8, crm_output%cldtop(icrm,:) * factor_xyt )
    crm_output%gicewp(icrm,:) = crm_output%gicewp(icrm,:)*crm_input%pdel(icrm,:)*1000./ggr * factor_xyt
    crm_output%gliqwp(icrm,:) = crm_output%gliqwp(icrm,:)*crm_input%pdel(icrm,:)*1000./ggr * factor_xyt
    crm_output%mcup  (icrm,:) = crm_output%mcup (icrm,:) * factor_xyt
    crm_output%mcdn  (icrm,:) = crm_output%mcdn (icrm,:) * factor_xyt
    crm_output%mcuup (icrm,:) = crm_output%mcuup(icrm,:) * factor_xyt
    crm_output%mcudn (icrm,:) = crm_output%mcudn(icrm,:) * factor_xyt
    crm_output%mctot (icrm,:) = crm_output%mcup(icrm,:) + crm_output%mcdn(icrm,:) + crm_output%mcuup(icrm,:) + crm_output%mcudn(icrm,:)

    crm_output%qc_mean(icrm,:) = crm_output%qc_mean(icrm,:) * factor_xy
    crm_output%qi_mean(icrm,:) = crm_output%qi_mean(icrm,:) * factor_xy
    crm_output%qs_mean(icrm,:) = crm_output%qs_mean(icrm,:) * factor_xy
    crm_output%qg_mean(icrm,:) = crm_output%qg_mean(icrm,:) * factor_xy
    crm_output%qr_mean(icrm,:) = crm_output%qr_mean(icrm,:) * factor_xy
#ifdef m2005
    crm_output%nc_mean(icrm,:) = crm_output%nc_mean(icrm,:) * factor_xy
    crm_output%ni_mean(icrm,:) = crm_output%ni_mean(icrm,:) * factor_xy
    crm_output%ns_mean(icrm,:) = crm_output%ns_mean(icrm,:) * factor_xy
    crm_output%ng_mean(icrm,:) = crm_output%ng_mean(icrm,:) * factor_xy
    crm_output%nr_mean(icrm,:) = crm_output%nr_mean(icrm,:) * factor_xy

    ! hm 8/31/11 new output, gcm-grid- and time-step avg
    ! add loop over i,j do get horizontal avg, and flip vertical array
    do k=1,nzm
      l = plev-k+1
      do j=1,ny
        do i=1,nx
          crm_output%aut_a (icrm,l) = crm_output%aut_a (icrm,l) + aut1a (i,j,k)
          crm_output%acc_a (icrm,l) = crm_output%acc_a (icrm,l) + acc1a (i,j,k)
          crm_output%evpc_a(icrm,l) = crm_output%evpc_a(icrm,l) + evpc1a(i,j,k)
          crm_output%evpr_a(icrm,l) = crm_output%evpr_a(icrm,l) + evpr1a(i,j,k)
          crm_output%mlt_a (icrm,l) = crm_output%mlt_a (icrm,l) + mlt1a (i,j,k)
          crm_output%sub_a (icrm,l) = crm_output%sub_a (icrm,l) + sub1a (i,j,k)
          crm_output%dep_a (icrm,l) = crm_output%dep_a (icrm,l) + dep1a (i,j,k)
          crm_output%con_a (icrm,l) = crm_output%con_a (icrm,l) + con1a (i,j,k)
        enddo
      enddo
    enddo

    ! note, rates are divded by dt to get mean rate over step
    crm_output%aut_a (icrm,:) = crm_output%aut_a (icrm,:) * factor_xyt / dt
    crm_output%acc_a (icrm,:) = crm_output%acc_a (icrm,:) * factor_xyt / dt
    crm_output%evpc_a(icrm,:) = crm_output%evpc_a(icrm,:) * factor_xyt / dt
    crm_output%evpr_a(icrm,:) = crm_output%evpr_a(icrm,:) * factor_xyt / dt
    crm_output%mlt_a (icrm,:) = crm_output%mlt_a (icrm,:) * factor_xyt / dt
    crm_output%sub_a (icrm,:) = crm_output%sub_a (icrm,:) * factor_xyt / dt
    crm_output%dep_a (icrm,:) = crm_output%dep_a (icrm,:) * factor_xyt / dt
    crm_output%con_a (icrm,:) = crm_output%con_a (icrm,:) * factor_xyt / dt
#endif /* m2005 */

    crm_output%precc (icrm) = 0.
    crm_output%precl (icrm) = 0.
    crm_output%precsc(icrm) = 0.
    crm_output%precsl(icrm) = 0.
    do j=1,ny
      do i=1,nx
#ifdef sam1mom
        precsfc(i,j) = precsfc(i,j)*dz/dt/dble(nstop)
        precssfc(i,j) = precssfc(i,j)*dz/dt/dble(nstop)
#endif /* sam1mom */
#ifdef m2005
        ! precsfc and precssfc from the subroutine of micro_proc in M2005 have a unit mm/s/dz
        !          precsfc(i,j) = precsfc(i,j)*dz/dble(nstop)     !mm/s/dz --> mm/s
        !          precssfc(i,j) = precssfc(i,j)*dz/dble(nstop)   !mm/s/dz --> mm/s
        ! precsfc and precssfc from the subroutine of micro_proc in M2005 have a unit mm/dz
        precsfc(i,j) = precsfc(i,j)*dz/dt/dble(nstop)     !mm/s/dz --> mm/s
        precssfc(i,j) = precssfc(i,j)*dz/dt/dble(nstop)   !mm/s/dz --> mm/s
#endif /* m2005 */
        if(precsfc(i,j).gt.10./86400.) then
           crm_output%precc (icrm) = crm_output%precc (icrm) + precsfc(i,j)
           crm_output%precsc(icrm) = crm_output%precsc(icrm) + precssfc(i,j)
        else
           crm_output%precl (icrm) = crm_output%precl (icrm) + precsfc(i,j)
           crm_output%precsl(icrm) = crm_output%precsl(icrm) + precssfc(i,j)
        endif
      enddo
    enddo
    crm_output%prec_crm(icrm,:,:) = precsfc/1000.           !mm/s --> m/s
    crm_output%precc   (icrm)     = crm_output%precc (icrm)*factor_xy/1000.
    crm_output%precl   (icrm)     = crm_output%precl (icrm)*factor_xy/1000.
    crm_output%precsc  (icrm)     = crm_output%precsc(icrm)*factor_xy/1000.
    crm_output%precsl  (icrm)     = crm_output%precsl(icrm)*factor_xy/1000.

!    !+++mhwangtest
!    ! test water conservtion problem
!    do k=1, nzm
!      l=plev-k+1
!      do j=1, ny
!        do i=1, nx
!#ifdef m2005
!          qtot(icrm,9) = qtot(icrm,9)+((micro_field(i,j,k,iqr)+micro_field(i,j,k,iqs)+micro_field(i,j,k,iqg)) * crm_input%pdel(icrm,l)/ggr)/(nx*ny)
!          qtot(icrm,9) = qtot(icrm,9)+((micro_field(i,j,k,iqv)+micro_field(i,j,k,iqci)) * crm_input%pdel(icrm,l)/ggr)/(nx*ny)
!#endif
!#ifdef sam1mom
!          qtot(icrm,9) = qtot(icrm,9)+((micro_field(i,j,k,1)+micro_field(i,j,k,2)) * crm_input%pdel(icrm,l)/ggr)/(nx*ny)
!#endif
!        enddo
!      enddo
!    enddo
!    qtot(icrm,9) = qtot(icrm,9) + (crm_output%precc(icrm)+crm_output%precl(icrm))*1000 * crm_run_time

    crm_output%cltot(icrm) = crm_output%cltot(icrm) * factor_xyt
    crm_output%clhgh(icrm) = crm_output%clhgh(icrm) * factor_xyt
    crm_output%clmed(icrm) = crm_output%clmed(icrm) * factor_xyt
    crm_output%cllow(icrm) = crm_output%cllow(icrm) * factor_xyt

    crm_output%jt_crm(icrm) = plev * 1.0
    crm_output%mx_crm(icrm) = 1.0
    do k=1, plev
      crm_output%mu_crm(icrm,k)=0.5*(mui_crm(icrm,k)+mui_crm(icrm,k+1))
      crm_output%md_crm(icrm,k)=0.5*(mdi_crm(icrm,k)+mdi_crm(icrm,k+1))
      crm_output%mu_crm(icrm,k)=crm_output%mu_crm(icrm,k)*ggr/100.          !kg/m2/s --> mb/s
      crm_output%md_crm(icrm,k)=crm_output%md_crm(icrm,k)*ggr/100.          !kg/m2/s --> mb/s
      crm_output%eu_crm(icrm,k) = 0.
      if(mui_crm(icrm,k)-mui_crm(icrm,k+1).gt.0) then
        crm_output%eu_crm(icrm,k)=(mui_crm(icrm,k)-mui_crm(icrm,k+1))*ggr/crm_input%pdel(icrm,k)    !/s
      else
        crm_output%du_crm(icrm,k)=-1.0*(mui_crm(icrm,k)-mui_crm(icrm,k+1))*ggr/crm_input%pdel(icrm,k)   !/s
      endif
      if(mdi_crm(icrm,k+1)-mdi_crm(icrm,k).lt.0) then
        crm_output%ed_crm(icrm,k)=(mdi_crm(icrm,k)-mdi_crm(icrm,k+1))*ggr/crm_input%pdel(icrm,k) ! /s
      else
        dd_crm(icrm,k)=-1.*(mdi_crm(icrm,k)-mdi_crm(icrm,k+1))*ggr/crm_input%pdel(icrm,k)   !/s
      endif
      if(abs(crm_output%mu_crm(icrm,k)).gt.1.0e-15.or.abs(crm_output%md_crm(icrm,k)).gt.1.0e-15) then
        crm_output%jt_crm(icrm) = min(k*1.0_r8, crm_output%jt_crm(icrm))
        crm_output%mx_crm(icrm) = max(k*1.0_r8, crm_output%mx_crm(icrm))
      endif
    enddo

    !-------------------------------------------------------------
    !       Fluxes and other stat:
    !-------------------------------------------------------------
    do k=1,nzm
      u2z = 0.
      v2z = 0.
      w2z = 0.
      do j=1,ny
        do i=1,nx
          u2z = u2z+(u(i,j,k)-u0(k))**2
          v2z = v2z+(v(i,j,k)-v0(k))**2
          w2z = w2z+0.5*(w(i,j,k+1)**2+w(i,j,k)**2)
        enddo
      enddo
      !+++mhwang
      ! mkwsb, mkle, mkadv, mkdiff (also crm_output%flux_u, crm_output%flux_v) seem not calculted correclty in the spcam3.5 codes.
      ! Only values at the last time step are calculated, but is averaged over the entire GCM
      ! time step.
      !---mhwang

      tmp1 = dz/rhow(k)
      tmp2 = tmp1/dtn                        ! dtn is calculated inside of the icyc loop.
                                             ! It seems wrong to use it here ???? +++mhwang
      mkwsb (k,:) = mkwsb (k,:) * tmp1*rhow(k) * factor_xy/nstop     !kg/m3/s --> kg/m2/s
      mkwle (k,:) = mkwle (k,:) * tmp2*rhow(k) * factor_xy/nstop     !kg/m3   --> kg/m2/s
      mkadv (k,:) = mkadv (k,:) * factor_xy*icrm_run_time     ! kg/kg  --> kg/kg/s
      mkdiff(k,:) = mkdiff(k,:) * factor_xy*icrm_run_time   ! kg/kg  --> kg/kg/s

      ! qpsrc, qpevp, qpfall in M2005 are calculated in micro_flux.
      qpsrc   (k) = qpsrc   (k) * factor_xy*icrm_run_time
      qpevp   (k) = qpevp   (k) * factor_xy*icrm_run_time
      qpfall  (k) = qpfall  (k) * factor_xy*icrm_run_time   ! kg/kg in M2005 ---> kg/kg/s
      precflux(k) = precflux(k) * factor_xy*dz/dt/nstop  !kg/m2/dz in M2005 -->kg/m2/s or mm/s (idt_gl=1/dt/nstop)

      l = plev-k+1
      crm_output%flux_u    (icrm,l) = (uwle(k) + uwsb(k))*tmp1*factor_xy/nstop
      crm_output%flux_v    (icrm,l) = (vwle(k) + vwsb(k))*tmp1*factor_xy/nstop
#ifdef sam1mom
      crm_output%flux_qt   (icrm,l) = mkwle(k,1) + mkwsb(k,1)
      crm_output%fluxsgs_qt(icrm,l) = mkwsb(k,1)
      crm_output%flux_qp   (icrm,l) = mkwle(k,2) + mkwsb(k,2)
      crm_output%qt_trans  (icrm,l) = mkadv(k,1) + mkdiff(k,1)
      crm_output%qp_trans  (icrm,l) = mkadv(k,2) + mkdiff(k,2)
#endif /* sam1mom */
#ifdef m2005
      crm_output%flux_qt   (icrm,l) = mkwle(k,1   ) + mkwsb(k,1   ) +  &
                         mkwle(k,iqci) + mkwsb(k,iqci)
      crm_output%fluxsgs_qt(icrm,l) = mkwsb(k,1   ) + mkwsb(k,iqci)
      crm_output%flux_qp   (icrm,l) = mkwle(k,iqr) + mkwsb(k,iqr) +  &
                         mkwle(k,iqs) + mkwsb(k,iqs) + mkwle(k,iqg) + mkwsb(k,iqg)
      crm_output%qt_trans  (icrm,l) = mkadv (k,1) + mkadv (k,iqci) + &
                         mkdiff(k,1) + mkdiff(k,iqci)
      crm_output%qp_trans  (icrm,l) = mkadv (k,iqr) + mkadv (k,iqs) + mkadv (k,iqg) + &
                         mkdiff(k,iqr) + mkdiff(k,iqs) + mkdiff(k,iqg)
#endif /* m2005 */
      crm_output%tkesgsz   (icrm,l)= rho(k)*sum(tke(1:nx,1:ny,k))*factor_xy
      crm_output%tkez      (icrm,l)= rho(k)*0.5*(u2z+v2z*YES3D+w2z)*factor_xy + crm_output%tkesgsz(icrm,l)
      crm_output%tkz       (icrm,l) = sum(tk(1:nx, 1:ny, k)) * factor_xy
      crm_output%precflux      (icrm,l) = precflux(k)/1000.       !mm/s  -->m/s

      crm_output%qp_fall   (icrm,l) = qpfall(k)
      crm_output%qp_evp    (icrm,l) = qpevp(k)
      crm_output%qp_src    (icrm,l) = qpsrc(k)

      crm_output%qt_ls     (icrm,l) = qtend(k)
      crm_output%t_ls      (icrm,l) = ttend(k)
    enddo

#ifdef ECPP
    abnd         (icrm,:,:,:,:)=0.0
    abnd_tf      (icrm,:,:,:,:)=0.0
    massflxbnd   (icrm,:,:,:,:)=0.0
    acen         (icrm,:,:,:,:)=0.0
    acen_tf      (icrm,:,:,:,:)=0.0
    rhcen        (icrm,:,:,:,:)=0.0
    qcloudcen    (icrm,:,:,:,:)=0.0
    qicecen      (icrm,:,:,:,:)=0.0
    qlsinkcen    (icrm,:,:,:,:)=0.0
    precrcen     (icrm,:,:,:,:)=0.0
    precsolidcen (icrm,:,:,:,:)=0.0
    qlsink_bfcen (icrm,:,:,:,:)=0.0
    qlsink_avgcen(icrm,:,:,:,:)=0.0
    praincen     (icrm,:,:,:,:)=0.0

    wupthresh_bnd   (icrm,:)=0.0
    wdownthresh_bnd (icrm,:)=0.0
    wwqui_cen       (icrm,:)=0.0
    wwqui_bnd       (icrm,:)=0.0
    wwqui_cloudy_cen(icrm,:)=0.0
    wwqui_cloudy_bnd(icrm,:)=0.0

    ! default is clear, non-precipitating, and quiescent class
    abnd   (icrm,:,1,1,1)=1.0
    abnd_tf(icrm,:,1,1,1)=1.0
    acen   (icrm,:,1,1,1)=1.0
    acen_tf(icrm,:,1,1,1)=1.0
    do k=1, nzm
      l=plev-k+1
      acen            (icrm,l,:,:,:) = area_cen_sum        (k,:,1:ncls_ecpp_in,:)
      acen_tf         (icrm,l,:,:,:) = area_cen_final      (k,:,1:ncls_ecpp_in,:)
      rhcen           (icrm,l,:,:,:) = rh_cen_sum          (k,:,1:ncls_ecpp_in,:)
      qcloudcen       (icrm,l,:,:,:) = qcloud_cen_sum      (k,:,1:ncls_ecpp_in,:)
      qicecen         (icrm,l,:,:,:) = qice_cen_sum        (k,:,1:ncls_ecpp_in,:)
      qlsinkcen       (icrm,l,:,:,:) = qlsink_cen_sum      (k,:,1:ncls_ecpp_in,:)
      precrcen        (icrm,l,:,:,:) = precr_cen_sum       (k,:,1:ncls_ecpp_in,:)
      precsolidcen    (icrm,l,:,:,:) = precsolid_cen_sum   (k,:,1:ncls_ecpp_in,:)
      wwqui_cen       (icrm,l)       = wwqui_cen_sum       (k)
      wwqui_cloudy_cen(icrm,l)       = wwqui_cloudy_cen_sum(k)
      qlsink_bfcen    (icrm,l,:,:,:) = qlsink_bf_cen_sum   (k,:,1:ncls_ecpp_in,:)
      qlsink_avgcen   (icrm,l,:,:,:) = qlsink_avg_cen_sum  (k,:,1:ncls_ecpp_in,:)
      praincen        (icrm,l,:,:,:) = prain_cen_sum       (k,:,1:ncls_ecpp_in,:)
    enddo
    do k=1, nzm+1
      l=plev+1-k+1
      abnd            (icrm,l,:,:,:) = area_bnd_sum        (k,:,1:ncls_ecpp_in,:)
      abnd_tf         (icrm,l,:,:,:) = area_bnd_final      (k,:,1:ncls_ecpp_in,:)
      massflxbnd      (icrm,l,:,:,:) = mass_bnd_sum        (k,:,1:ncls_ecpp_in,:)
      wupthresh_bnd   (icrm,l)       = wup_thresh          (k)
      wdownthresh_bnd (icrm,l)       = wdown_thresh        (k)
      wwqui_bnd       (icrm,l)       = wwqui_bnd_sum       (k)
      wwqui_cloudy_bnd(icrm,l)       = wwqui_cloudy_bnd_sum(k)
    enddo
#endif /* ECPP */

    crm_output%timing_factor(icrm) = crm_output%timing_factor(icrm) / nstop

#ifdef CLUBB_CRM
    ! Deallocate CLUBB variables, etc.
    ! -UWM
    if ( doclubb .or. doclubbnoninter ) call clubb_sgs_cleanup( )
#endif
#ifdef ECPP
    ! Deallocate ECPP variables
    call ecpp_crm_cleanup ()
#endif

  enddo

  call deallocate_grid()
  call deallocate_tracers()
  call deallocate_sgs()
  call deallocate_vars()
#ifdef sam1mom
  call deallocate_micro_params()
  call deallocate_micro()
#endif
#if defined( SP_ESMT )
  call deallocate_scalar_momentum()
#endif

end subroutine crm

end module crm_module
