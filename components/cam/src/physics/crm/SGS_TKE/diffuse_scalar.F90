module diffuse_scalar_mod
  use diffuse_scalar2D_mod
  use diffuse_scalar3D_mod
  implicit none

contains

  subroutine diffuse_scalar (dimx1_d,dimx2_d,dimy1_d,dimy2_d,grdf_x,grdf_y,grdf_z,tkh,f,fluxb,fluxt,fdiff,flux,f2lediff,f2lediss,fwlediff,doit,ncrms)

    use grid
    use vars, only: rho, rhow
    use params
    implicit none
    integer, intent(in) :: ncrms

    ! input:
    integer :: dimx1_d,dimx2_d,dimy1_d,dimy2_d
    real(crm_rknd) grdf_x  (ncrms,nzm)! grid factor for eddy diffusion in x
    real(crm_rknd) grdf_y  (ncrms,nzm)! grid factor for eddy diffusion in y
    real(crm_rknd) grdf_z  (ncrms,nzm)! grid factor for eddy diffusion in z
    real(crm_rknd) tkh     (ncrms,dimx1_d:dimx2_d, dimy1_d:dimy2_d, nzm) ! SGS eddy conductivity
    real(crm_rknd) f       (ncrms,dimx1_s:dimx2_s, dimy1_s:dimy2_s, nzm)	! scalar
    real(crm_rknd) fluxb   (ncrms,nx,ny)		! bottom flux
    real(crm_rknd) fluxt   (ncrms,nx,ny)		! top flux
    real(crm_rknd) flux    (ncrms,nz)
    real(crm_rknd) f2lediff(ncrms,nz)
    real(crm_rknd) f2lediss(ncrms,nz)
    real(crm_rknd) fwlediff(ncrms,nz)
    real(crm_rknd) fdiff   (ncrms,nz)
    logical doit
    ! Local
    real(crm_rknd) df(ncrms,dimx1_s:dimx2_s, dimy1_s:dimy2_s, nzm)	! scalar
    integer i,j,k

    !call t_startf ('diffuse_scalars')

    df(:,:,:,:) = f(:,:,:,:)

    if(RUN3D) then
      call diffuse_scalar3D (dimx1_d,dimx2_d,dimy1_d,dimy2_d,grdf_x,grdf_y,grdf_z,f,fluxb,fluxt,tkh,rho,rhow,flux,ncrms)
    else
      call diffuse_scalar2D (dimx1_d,dimx2_d,dimy1_d,dimy2_d,grdf_x,grdf_y,       f,fluxb,fluxt,tkh,rho,rhow,flux,ncrms)
    endif

    do k=1,nzm
      fdiff(:,k)=0.
      do j=1,ny
        do i=1,nx
          fdiff(:,k)=fdiff(:,k)+f(:,i,j,k)-df(:,i,j,k)
        end do
      end do
    end do

    !call t_stopf ('diffuse_scalars')

  end subroutine diffuse_scalar

end module diffuse_scalar_mod
