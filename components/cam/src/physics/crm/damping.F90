module damping_mod
  use task_util_mod
  implicit none

contains

  subroutine damping(ncrms,icrm)

    !  "Spange"-layer damping at the domain top region

    use vars
    use microphysics, only: micro_field, index_water_vapor
    use params, only: crm_rknd
    implicit none
    integer, intent(in) :: ncrms, icrm

    real(crm_rknd) tau_min  ! minimum damping time-scale (at the top)
    real(crm_rknd) tau_max    ! maxim damping time-scale (base of damping layer)
    real(crm_rknd) damp_depth ! damping depth as a fraction of the domain height
    parameter(tau_min=60., tau_max=450., damp_depth=0.4)
    real(crm_rknd) tau(nzm)
    integer i, j, k, n_damp

    if(tau_min.lt.2*dt) then
      print*,'Error: in damping() tau_min is too small!'
      call task_abort()
    end if

    do k=nzm,1,-1
      if(z(icrm,nzm)-z(icrm,k).lt.damp_depth*z(icrm,nzm)) then
        n_damp=nzm-k+1
      endif
    end do

    do k=nzm,nzm-n_damp,-1
      tau(k) = tau_min *(tau_max/tau_min)**((z(icrm,nzm)-z(icrm,k))/(z(icrm,nzm)-z(icrm,nzm-n_damp)))
      tau(k)=1./tau(k)
    end do

    !+++mhwang recalculate grid-mean u0, v0, t0 first,
    ! as t have been updated. No need for qv0, as
    ! qv has not been updated yet the calculation of qv0.
    do k=1, nzm
      u0(icrm,k)=0.0
      v0(icrm,k)=0.0
      t0(icrm,k)=0.0
      do j=1, ny
        do i=1, nx
          u0(icrm,k) = u0(icrm,k) + u(icrm,i,j,k)/(nx*ny)
          v0(icrm,k) = v0(icrm,k) + v(icrm,i,j,k)/(nx*ny)
          t0(icrm,k) = t0(icrm,k) + t(icrm,i,j,k)/(nx*ny)
        end do
      end do
    end do
    !---mhwang

    do k = nzm, nzm-n_damp, -1
      do j=1,ny
        do i=1,nx
          dudt(icrm,i,j,k,na)= dudt(icrm,i,j,k,na)-(u(icrm,i,j,k)-u0(icrm,k)) * tau(k)
          dvdt(icrm,i,j,k,na)= dvdt(icrm,i,j,k,na)-(v(icrm,i,j,k)-v0(icrm,k)) * tau(k)
          dwdt(icrm,i,j,k,na)= dwdt(icrm,i,j,k,na)-w(icrm,i,j,k) * tau(k)
          t(icrm,i,j,k)= t(icrm,i,j,k)-dtn*(t(icrm,i,j,k)-t0(icrm,k)) * tau(k)
          ! In the old version (SAM7.5?) of SAM, water vapor is the prognostic variable for the two-moment microphyscs.
          ! So the following damping approach can lead to the negative water vapor.
          !      micro_field(icrm,i,j,k,index_water_vapor)= micro_field(icrm,i,j,k,index_water_vapor)- &
          !                                    dtn*(qv(icrm,i,j,k)+qcl(icrm,i,j,k)+qci(icrm,i,j,k)-q0(icrm,k)) * tau(k)
          ! a simple fix (Minghuai Wang, 2011-08):
          micro_field(icrm,i,j,k,index_water_vapor)= micro_field(icrm,i,j,k,index_water_vapor)- &
          dtn*(qv(icrm,i,j,k)-qv0(icrm,k)) * tau(k)
        end do! i
      end do! j
    end do ! k

  end subroutine damping

end module damping_mod
