module setperturb_mod
  use random_mod
  implicit none

contains

  subroutine setperturb(iseed,ncrms,icrm)

    !  Random noise
    !  This surboutine has been updated for SPCAM5 (Minghuai.Wang@pnnl.gov, April, 2012).
    !  Now the random generator is seeded based on the global column id, which gets rid
    !  of the dependence of the SPCAM results on pcols.

    use vars
    use sgs, only: setperturb_sgs
    use params, only: crm_rknd

    implicit none
    integer, intent(in) :: ncrms,icrm

    integer, intent(in) :: iseed

    integer i,j,k
    real(crm_rknd) rrr
    integer, allocatable :: rndm_seed(:)
    integer :: rndm_seed_sz
    real(crm_rknd) :: t02(nzm)
    real(crm_rknd) :: tke02(nzm)

    !call ranset_(30*rank)
    call random_seed(size=rndm_seed_sz)
    allocate(rndm_seed(rndm_seed_sz))

    rndm_seed = iseed
    call random_seed(put=rndm_seed)

    call setperturb_sgs(0,ncrms,icrm)  ! set sgs fields

    t02 = 0.0
    tke02 = 0.0
    do k=1,nzm
      do j=1,ny
        do i=1,nx
          rrr=1.-2.*ranf_()

          if(k.le.5) then
            t(icrm,i,j,k)=t(icrm,i,j,k)+0.02*rrr*(6-k)
          endif
          t02(k) = t02(k) + t(icrm,i,j,k)/(nx*ny)
        end do
      end do

      ! energy conservation +++mhwang (2012-06)
      do j=1, ny
        do i=1, nx
          if(k.le.5) then
            t(icrm,i,j,k) = t(icrm,i,j,k) * t0(icrm,k)/t02(k)
          end if
        end do
      end do
    end do

    deallocate(rndm_seed)

  end

end module setperturb_mod
